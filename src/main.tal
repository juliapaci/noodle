( Noodle )

~src/utils.tal

( variables )

|0000

@state
	&timer $1 &changed $1
@brush
	&tool $1 &size $1 &patt $1 &oper $2 &mode $1
@cursor
	&x1 $2 &y1 $2 &x2 $2 &y2 $2 &last $1 &drag $1
@zoom
	&active $1 &x $2 &y $2
( views )
@panel
	&x $2 &y $2 &x2 $2 &y2 $2 &drag-event $2
@viewport
	&x $2 &y $2 &x2 $2 &y2 $2 &size &w $1 &h $1
@viewport-handle
	&x $2 &y $2 &x2 $2 &y2 $2 &drag-event $2
@viewport-label
	&x $2 &y $2 &x2 $2 &y2 $2 &drag-event $2
@surface
	&x $2 &y $2 &x2 $2 &y2 $2 &size &w $1 &h $1
@surface-handle
	&x $2 &y $2 &x2 $2 &y2 $2 &drag-event $2

@filepath $40

@guide
	&x $2 &y $2 &x2 $2 &y2 $2
@line
	&x1 $2 &y1 $2 &x2 $2 &y2 $2 &sx $2 &sy $2 &dx $2 &dy $2 &e1 $2 &e2 $2
@line-guide
	&x1 $2 &y1 $2 &x2 $2 &y2 $2 &sx $2 &sy $2 &dx $2 &dy $2 &e1 $2 &e2 $2

( program )

|0100

	( vectors )
	;on-console .Console/vector DEO2
	( connect drag events )
	;on-drag-panel .panel/drag-event STZ2
	;on-drag-viewport-handle .viewport-handle/drag-event STZ2
	;on-drag-viewport-label .viewport-label/drag-event STZ2
	;on-drag-surface-handle .surface-handle/drag-event STZ2
	( colors )
	#70fe .System/r DEO2
	#c0f3 .System/g DEO2
	#90f3 .System/b DEO2
	( theme support )
	;load-theme JSR2
	( default brush )
	#04 .brush/size STZ
	#00 .brush/patt STZ
	#00 .brush/tool STZ
	( initial position )
	#0008 #0008 ;set-panel-position JSR2
	#0028 #0012 ;set-viewport-position JSR2
	.Screen/width DEI2 8// NIP #07 - .Screen/height DEI2 8// NIP #04 - ;set-viewport-size JSR2
	.viewport/w LDZ #02 - .viewport/h LDZ #02 - ;set-surface-size JSR2
	( let's begin. )
	;untrap JSR2
	;new-file JSR2

BRK

@untrap ( -- )

	( vectors )
	;on-frame .Screen/vector DEO2
	;on-button .Controller/vector DEO2
	;on-mouse .Mouse/vector DEO2
	( filepath )
	#06 ;draw-filepath JSR2
	RELEASE-MOUSE

RTN

@trap ( -- )

	( vectors )
	;on-frame-trap .Screen/vector DEO2
	;on-button-trap .Controller/vector DEO2
	;on-mouse-trap .Mouse/vector DEO2
	( filepath )
	#06 ;draw-filepath JSR2
	RELEASE-MOUSE
	( clear brush )
	.cursor/x2 LDZ2 .Screen/x DEO2
	.cursor/y2 LDZ2 .Screen/y DEO2
	#40 .Screen/sprite DEO

RTN

@on-console ( -> )

	#00 ;draw-filepath JSR2
	( start )
	[ LIT &listening $1 ] ,&no-start JCN
		#01 ,&listening STR
		;filepath #0040 ;mclr JSR2
		&no-start
	( end )
	.Console/read DEI
	DUP IS-VALID ,&no-end JCN
		#00 ,&listening STR
		&no-end
	( capture )
	,capture-trap JSR
	#06 ;draw-filepath JSR2

BRK

@on-button-trap ( -> )

	#00 ;draw-filepath JSR2
	.Controller/key DEI STANDARD-LB ,capture-trap JSR
	#06 ;draw-filepath JSR2

BRK

@capture-trap ( button -- )

	DUP ,&no-null JCN POP RTN &no-null
	[ #08 ] !~ ,&no-pop JCN ;filepath ;spop JSR2 POP RTN &no-pop
	[ #0a ] !~ ,&no-load JCN ;load-file JSR2 &no-load
	[ #7f ] !~ ,&no-delete JCN ;filepath #0040 ;mclr JSR2 POP RTN &no-delete
	[ #20 ] >~ ,&no-special JCN ;untrap JSR2 POP RTN &no-special
	;filepath ;slen JSR2 NIP #3f = ,&no-push JCN
		DUP ;filepath ROT ;sput JSR2
		&no-push
	POP

RTN

@on-frame-trap ( -> )

	.state/timer LDZ
	DUP 8MOD ,&no-blink JCN
		DUP 8/ 2MOD 2* #06 + ;draw-filepath JSR2
		&no-blink
	INC .state/timer STZ

BRK

@on-frame ( -> )

	( inactive )

BRK

@on-mouse-trap ( -> )

	( release trap on touch )
	.Mouse/state DEI #00 ! BRK?

	;untrap JSR2

BRK

@on-mouse ( -> )

	;draw-cursor JSR2
	( when drag&release )
	.cursor/drag LDZ #00 = ,&no-release JCN
	.Mouse/state DEI #00 ! ,&no-release JCN
		;stop-drag JSR2 BRK
		&no-release
	( route touch )
	.Mouse/x DEI2 .Mouse/y DEI2
	( on drag )
	.cursor/drag LDZ #00 > .Mouse/state DEI #00 > AND ,on-drag JCN
	( on panel )
	OVR2 OVR2 .panel ;within-rect JSR2 ;on-touch-panel JCN2
	.zoom/active LDZ ,&skip-handles JCN
		OVR2 OVR2 .viewport-handle ;within-rect JSR2 ;on-touch-viewport-handle JCN2
		OVR2 OVR2 .viewport-label ;within-rect JSR2 ;on-touch-viewport-label JCN2
		OVR2 OVR2 .surface-handle ;within-rect JSR2 ;on-touch-surface-handle JCN2
		OVR2 OVR2 .surface ;within-rect JSR2 ;on-touch-surface JCN2
		&skip-handles
	( on default )
	OVR2 OVR2 .viewport ;within-rect JSR2 ;on-touch-viewport JCN2
	POP2 POP2

BRK

( drag handlers )

@stop-drag ( -- )

	( release )
	#00 .cursor/drag STZ

RTN

@on-drag ( x* y* -> )

	( get event )
	.cursor/drag LDZ #08 + LDZ2	JMP2

BRK

@start-drag ( rect -- )

	( set frame )
	.cursor/drag STZ
	.Mouse/x DEI2 .cursor/x1 STZ2
	.Mouse/y DEI2 .cursor/y1 STZ2

RTN

@on-drag-panel ( x* y* -> )

	;clear-panel JSR2
	;draw-viewport JSR2
	DUP2 .cursor/y1 LDZ2 -- STH2 .cursor/y1 STZ2
		.panel/y LDZ2 STH2r ++ STH2
	DUP2 .cursor/x1 LDZ2 -- STH2 .cursor/x1 STZ2
		.panel/x LDZ2 STH2r ++ STH2r
		;set-panel-position JSR2

BRK

@on-drag-viewport-handle ( x* y* -> )

	.viewport/y LDZ2 -- #0008 ++ 8// NIP STH
	.viewport/x LDZ2 -- #0008 ++ 8// NIP STHr
		;set-viewport-size JSR2
	;draw-panel JSR2

BRK

@on-drag-viewport-label ( x* y* -> )

	;clear-viewport JSR2
	DUP2 .cursor/y1 LDZ2 -- STH2 .cursor/y1 STZ2
		.viewport/y LDZ2 STH2r ++ STH2
	DUP2 .cursor/x1 LDZ2 -- STH2 .cursor/x1 STZ2
		.viewport/x LDZ2 STH2r ++ STH2r
		;set-viewport-position JSR2
	;draw-panel JSR2

BRK

@on-drag-surface-handle ( x* y* -> )

	#00 .viewport/h LDZ 2/ 8** .viewport/y LDZ2 ++ -- 4// NIP STH
	#00 .viewport/w LDZ 2/ 8** .viewport/x LDZ2 ++ -- 4// NIP STHr
		;set-surface-size JSR2

BRK

@on-touch-panel ( x* y* -> )

	.Mouse/state DEI ,&on-click JCN
		POP2 POP2 BRK
		&on-click
	.panel/y LDZ2 #0003 ++ -- #000a // NIP STH
	.panel/x LDZ2 -- #000a // NIP STHr
	( within x )
	OVR #02 < ,&valid JCN POP2 BRK &valid
	( within y )
	DUP #03 = ,&drag JCN
	DUP #08 = ,&drag JCN
	( release when not drag )
	RELEASE-MOUSE
	DUP #03 < ,&tool JCN
	DUP #03 > OVR #08 < AND ,&patt JCN
	DUP #07 > OVR #0d < AND ,&size JCN

BRK
	&tool 2* + ;select-tool JSR2 BRK
	&patt #04 - 2* + ;select-patt JSR2 BRK
	&size #09 - 2* + ;select-size JSR2 BRK
	&drag POP2 .panel ;start-drag JSR2 BRK

@get-tool ( -- ptr* )

	( set erase from right-click/mode )
	[ ;remove-pixel ;add-pixel ] .Mouse/state DEI #01 > .brush/mode LDZ + #01 JCN SWP2 POP2 .brush/oper STZ2
	( get function )
	#00 .Mouse/state DEI #00 > 2* .cursor/last LDZ #00 > + 2* .brush/tool LDZ 8* + ;tools ++ LDA2
	( record state )
	.Mouse/state DEI .cursor/last STZ

RTN

@on-touch-surface ( x* y* -> )

	,get-tool JSR STH2
	( canvas offset )
	.surface/y LDZ2 -- SWP2
	.surface/x LDZ2 -- SWP2
		STH2r JMP2

BRK

@on-touch-viewport ( x* y* -- )

	.zoom/active LDZ #00 = ,&cancel JCN
	,get-tool JSR STH2
	( canvas offset )
	.viewport/y LDZ2 -- 8// .zoom/y LDZ2 ++ SWP2
	.viewport/x LDZ2 -- 8// .zoom/x LDZ2 ++ SWP2
		STH2r JMP2

BRK
	&cancel POP2 POP2 BRK

@on-touch-viewport-handle ( x* y* -- )

	POP2 POP2
	.Mouse/state DEI #00 ! BRK?
	.viewport-handle ;start-drag JSR2

BRK

@on-touch-viewport-label ( x* y* -- )

	POP2 POP2
	.Mouse/state DEI #00 ! BRK?
	.viewport-label ;start-drag JSR2

BRK

@on-touch-surface-handle ( x* y* -- )

	POP2 POP2
	.Mouse/state DEI #00 ! BRK?
	.surface-handle ;start-drag JSR2

BRK

@on-button ( -> )

	.Controller/button DEI
		DUP #01 AND ;on-button-ctrl JCN2
		DUP #f0 AND ;on-button-arrow JCN2
		DUP #04 ! ,&no-shift JCN
			;toggle-zoom JSR2
			&no-shift
		DUP #08 ! ,&no-esc JCN
			.brush/mode TOGGLE
			;draw-sizepane JSR2
			&no-esc
	POP

	.Controller/key DEI
	#00 !~ ,&continue JCN POP BRK &continue
	#09 !~ ,&no-invert JCN POP ;invert JSR2 BRK &no-invert
	#20 !~ ,&no-space JCN ;toggle-zoom JSR2 &no-space
	LIT 'q !~ ,&no-qkey JCN #00 ;select-tool JSR2 &no-qkey
	LIT 'w !~ ,&no-wkey JCN #01 ;select-tool JSR2 &no-wkey
	LIT 'e !~ ,&no-ekey JCN #02 ;select-tool JSR2 &no-ekey
	LIT 'r !~ ,&no-rkey JCN #03 ;select-tool JSR2 &no-rkey
	LIT 't !~ ,&no-tkey JCN #04 ;select-tool JSR2 &no-tkey
	LIT 'y !~ ,&no-ykey JCN #05 ;select-tool JSR2 &no-ykey
	LIT '[ !~ ,&no-lbracket JCN .brush/size LDZ #01 - ;select-size JSR2 &no-lbracket
	LIT '] !~ ,&no-rbracket JCN .brush/size LDZ INC ;select-size JSR2 &no-rbracket
	DUP
		DUP #30 > SWP #39 < #0101 !! ,&no-numkey JCN
		( size ) DUP #31 SUB ;select-patt JSR2 &no-numkey
	POP

BRK

@on-button-ctrl ( button -> )

	POP
	.Controller/key DEI
	( new/rename/open/save )
	[ LIT 'n ] !~ ,&no-new JCN ;new-file JSR2 POP BRK &no-new
	[ LIT 'r ] !~ ,&no-name JCN ;trap JSR2 POP BRK &no-name
	[ LIT 'o ] !~ ,&no-open JCN ;load-file JSR2 POP BRK &no-open
	[ LIT 's ] !~ ,&no-save JCN ;save-file JSR2 POP BRK  &no-save
	( copy glyph )
	[ LIT 'p ] !~ ,&no-copy JCN ;glyph-snarf JSR2 POP BRK  &no-copy
	POP

BRK

@on-button-arrow ( button -> )

	.zoom/active LDZ ,&is-zoom JCN POP BRK &is-zoom

	DUP #10 AND #00 = ,&no-u JCN .zoom/y LDZ2k DEC2 ROT STZ2 &no-u
	DUP #20 AND #00 = ,&no-d JCN .zoom/y LDZ2k INC2 ROT STZ2 &no-d
	DUP #40 AND #00 = ,&no-l JCN .zoom/x LDZ2k DEC2 ROT STZ2 &no-l
	DUP #80 AND #00 = ,&no-r JCN .zoom/x LDZ2k INC2 ROT STZ2 &no-r
	POP
	;draw-surface JSR2

BRK

( helpers )

@select-tool ( tool -- )

	.brush/tool STZ
	;draw-toolpane JSR2

RTN

@select-patt ( patt -- )

	8MOD
	.brush/patt STZ
	( select brush )
	.brush/tool LDZ ,&no-switch JCN
		#01 ,select-tool JSR
		&no-switch
	;draw-pattpane JSR2

RTN

@select-size ( size -- )

	8MOD
	( toggle mode on reselection )
	DUP .brush/size LDZ ! ,&no-toggle JCN
		.brush/mode TOGGLE
		&no-toggle
	.brush/size STZ
	( select brush )
	.brush/tool LDZ ,&no-switch JCN
		#01 ,select-tool JSR
		&no-switch
	;draw-sizepane JSR2

RTN

@toggle-zoom ( -- )

	.zoom/active TOGGLE
	;draw-viewport JSR2
	;draw-panel JSR2

RTN

@invert ( -- )

	;data STH2k GET-FRAME-SIZE ++ STH2r
	&loop
		( load ) STH2k LDAk #ff EOR
		( save ) STH2r STA
		INC2 GTH2k ,&loop JCN
	POP2 POP2
	;draw-surface JSR2

RTN

@set-viewport-size ( w h -- )
	
	( only redraw on change )
	DUP2 .viewport/size LDZ2 == ,&cancel JCN
	( limit size )
	DUP #01 < ,&cancel JCN
	OVR #01 < ,&cancel JCN
	DUP .Screen/height DEI2 8// NIP #04 - > ,&cancel JCN
	OVR .Screen/width DEI2 8// NIP #07 - > ,&cancel JCN

	;clear-viewport JSR2
	DUP .viewport/h STZ TOS 8** .viewport/y LDZ2 ++ .viewport/y2 STZ2
	DUP .viewport/w STZ TOS 8** .viewport/x LDZ2 ++ .viewport/x2 STZ2
	;center-surface JSR2

RTN
	&cancel POP2 RTN

@set-panel-position ( x* y* -- )

	DUP2 .panel/y STZ2 #0088 ++ .panel/y2 STZ2
	DUP2 .panel/x STZ2 #0018 ++ .panel/x2 STZ2
	;draw-panel JSR2

RTN

@set-viewport-position ( x* y* -- )

	DUP2 .viewport/y STZ2 #00 .viewport/h LDZ 8** ++ .viewport/y2 STZ2
	DUP2 .viewport/x STZ2 #00 .viewport/w LDZ 8** ++ .viewport/x2 STZ2
	;center-surface JSR2

RTN

@set-surface-size ( w h -- )

	( only redraw on change )
	DUP2 .surface/size LDZ2 == ,&cancel JCN
	( limit size )
	DUP #01 < ,&cancel JCN
	OVR #01 < ,&cancel JCN
	DUP #7f > ,&cancel JCN
	OVR #7f > ,&cancel JCN

	.surface/h STZ
	.surface/w STZ

	,center-surface JSR

RTN
	&cancel POP2 RTN

@center-surface ( -- )

	.viewport/w LDZ .surface/w LDZ INC > ,&center-x JCN
		.viewport/x LDZ2 .surface/x STZ2
		.viewport/x2 LDZ2 .surface/x2 STZ2
		,&resume-x JMP
		&center-x
	( center )
	.viewport/x LDZ2 STH2k
		.viewport/x2 LDZ2 STH2r -- #00 .surface/w LDZ 8** STH2k -- 2// ++
		DUP2 .surface/x STZ2
			STH2r DEC2 ++ .surface/x2 STZ2
	&resume-x
	.viewport/h LDZ .surface/h LDZ INC > ,&center-y JCN
		.viewport/y LDZ2 .surface/y STZ2
		.viewport/y2 LDZ2 .surface/y2 STZ2
		,&resume-y JMP
		&center-y
	.viewport/y LDZ2 STH2k
		.viewport/y2 LDZ2 STH2r -- #00 .surface/h LDZ 8** STH2k -- 2// ++
		DUP2 .surface/y STZ2
			STH2r DEC2 ++ .surface/y2 STZ2
	&resume-y
	( viewport handle )
	.viewport/x2 LDZ2 #0008 -- DUP2 .viewport-handle/x STZ2 #0008 ++ .viewport-handle/x2 STZ2
	.viewport/y2 LDZ2 #0008 -- DUP2 .viewport-handle/y STZ2 #0008 ++ .viewport-handle/y2 STZ2
	( surface handle )
	.surface/x2 LDZ2 DUP2 .surface-handle/x STZ2 #0008 ++ .surface-handle/x2 STZ2
	.surface/y2 LDZ2 DUP2 .surface-handle/y STZ2 #0008 ++ .surface-handle/y2 STZ2
	( label handle )
	.viewport/x LDZ2 .viewport-label/x STZ2
	.viewport/x2 LDZ2 .viewport-label/x2 STZ2
	.viewport/y LDZ2 DUP2 #0008 -- .viewport-label/y STZ2 .viewport-label/y2 STZ2
	;draw-viewport JSR2

RTN

@paint-line ( x1* y1* x2* y2* -- )

	( load ) .line/y1 STZ2 .line/x1 STZ2 .line/y2 STZ2 .line/x2 STZ2

	.line/x1 LDZ2 .line/x2 LDZ2 -- ABS2 .line/dx STZ2
	#0000 .line/y1 LDZ2 .line/y2 LDZ2 -- ABS2 -- .line/dy STZ2
	#ffff #00 .line/x2 LDZ2 .line/x1 LDZ2 LTS2 2** ++ .line/sx STZ2
	#ffff #00 .line/y2 LDZ2 .line/y1 LDZ2 LTS2 2** ++ .line/sy STZ2
	.line/dx LDZ2 .line/dy LDZ2 ++ .line/e1 STZ2
	&loop
		( paint ) .line/x2 LDZ2 .line/y2 LDZ2 .brush/oper LDZ2 JSR2
		.line/x2 LDZ2 .line/y2 LDZ2 ;draw-tile JSR2
		.line/x2 LDZ2 .line/x1 LDZ2 == .line/y2 LDZ2 .line/y1 LDZ2 == AND ,&end JCN
		.line/e1 LDZ2 2** .line/e2 STZ2
		.line/e2 LDZ2 .line/dy LDZ2 LTS2 ,&skipy JCN
			.line/e1 LDZ2 .line/dy LDZ2 ++ .line/e1 STZ2
			.line/x2 LDZ2 .line/sx LDZ2 ++ .line/x2 STZ2
			&skipy
		.line/e2 LDZ2 .line/dx LDZ2 GTS2 ,&skipx JCN
			.line/e1 LDZ2 .line/dx LDZ2 ++ .line/e1 STZ2
			.line/y2 LDZ2 .line/sy LDZ2 ++ .line/y2 STZ2
			&skipx
		;&loop JMP2
	&end

RTN

@paint-rect ( -- )

	.cursor/y2 LDZ2 .cursor/y1 LDZ2 ( flip sign ) GTH2k JMP SWP2
	&ver
		STH2k
		.cursor/x2 LDZ2 .cursor/x1 LDZ2 ( flip sign ) GTH2k JMP SWP2
		&hor
			DUP2 STH2kr ;patternize JSR2 #00 = ,&no-pixel JCN
				DUP2 STH2kr .brush/oper LDZ2 JSR2
				&no-pixel
			INC2 OVR2 OVR2 GTS2 ,&hor JCN
		POP2 POP2 POP2r
		INC2 OVR2 OVR2 GTS2 ,&ver JCN
	POP2 POP2
	;draw-surface JSR2

RTN

@paint-brush ( x* y* -- )

	STH2 STH2
	#08 #00
	&ver
		#08 #00
		&hor
			GET-ITER
			DUP2 STH STH ;size-icns #00 .brush/size LDZ 8** ++ #00 STHr ++ LDA #07 STHr SUB SFT #01 AND #00 = ,&skip JCN
			DUP2 [ TOS ROT TOS SWP2 ] STH2kr SWP2r STH2kr SWP2r ADD-POSITIONS OVR2 OVR2 ;patternize JSR2 #00 = ,&no-pixel JCN
					OVR2 OVR2 .brush/oper LDZ2 JSR2
					;draw-tile JSR2
					,&skip JMP
					&no-pixel
					POP2 POP2
				&skip
				POP2
			INC GTHk ,&hor JCN
			POP2
		INC GTHk ,&ver JCN
	POP2
	POP2r POP2r

RTN

@patternize ( x* y* -- )

	STH2 STH2
	.brush/patt LDZ
	DUP ,&noplain JCN
		POP POP2r POP2r
		#01 RTN &noplain
	DUP #01 ! ,&notone1 JCN
		POP OVR2r OVR2r
		STH2r STH2r ++ #0001 AND2 #0000 ==
		STH2r STH2r -- #0001 AND2 #0000 ==
		AND RTN &notone1
	DUP #02 ! ,&notone2 JCN
		POP OVR2r OVR2r
		STH2r STH2r ++ #0003 AND2 #0000 ==
		STH2r STH2r -- #0003 AND2 #0000 ==
		AND RTN &notone2
	DUP #03 ! ,&notone3 JCN
		POP OVR2r OVR2r
		STH2r STH2r ++ #0005 AND2 #0000 ==
		STH2r STH2r -- #0005 AND2 #0000 ==
		AND RTN &notone3
	DUP #04 ! ,&notone4 JCN
		POP STH2r STH2r
		++ #0003 AND2 #0000 == RTN &notone4
	DUP #05 ! ,&notone5 JCN
		POP STH2r STH2r
		-- #0003 AND2 #0000 == RTN &notone5
	DUP #06 ! ,&notone6 JCN
		POP SWP2r POP2r STH2r
		#0001 AND2 NIP RTN &notone6
	DUP #07 ! ,&notone7 JCN
		POP POP2r STH2r
		#0001 AND2 NIP RTN &notone7
	POP POP2r POP2r
	#00

RTN

@paint-magic ( x* y* -- )

	STH2 STH2
	.brush/size LDZ #00
	&ver
		.brush/size LDZ #00
		&hor
			GET-ITER [ TOS ROT TOS ]
			STH2kr SWP2r STH2kr SWP2r ADD-POSITIONS
			( skip blanks )
			OVR2 OVR2 ;get-pixel JSR2 #00 = ,&blank JCN
				OVR2 OVR2 ,clean-pixel JSR
				OVR2 OVR2 ;draw-tile JSR2
				&blank
			POP2 POP2
			INC GTHk ,&hor JCN
			POP2
		INC GTHk ,&ver JCN
	POP2
	POP2r POP2r

RTN

@clean-pixel ( x* y* -- )

	( get neighbors )
	OVR2 OVR2 DEC2 ,get-pixel JSR STH
	OVR2 INC2 OVR2 ,get-pixel JSR #10 SFT STH ADDr
	OVR2 OVR2 INC2 ,get-pixel JSR #20 SFT STH ADDr
	OVR2 DEC2 OVR2 ,get-pixel JSR #30 SFT STH ADDr
	STHr
	DUP #03 = ,&remove JCN
	DUP #06 = ,&remove JCN
	DUP #09 = ,&remove JCN
	DUP #0c = ,&remove JCN
	POP POP2 POP2 RTN
	&remove
		POP ;remove-pixel JSR2 RTN

RTN

@get-pixel ( x* y* -- b )

	,within-surface JSR ,&continue JCN
		POP2 POP2 #01 RTN
		&continue

	( keep x* ) OVR2 NIP 8MOD STH
	( get byte ) ,get-row JSR LDA
	( flag ) #07 STHr SUB SFT #01 AND

RTN

@set-pixel ( x* y* color -- )

	,add-pixel JCN
	,remove-pixel JMP

RTN

@add-pixel ( x* y* -- )

	,within-surface JSR ,&continue JCN
		POP2 POP2 RTN
		&continue

	( keep x* ) OVR2 NIP 8MOD STH
	( get byte ) ,get-row JSR LDAk
	( mask ) #01 #07 STHr SUB SFL ORA
	( save ) ROT ROT STA

RTN

@remove-pixel ( x* y* -- )

	,within-surface JSR ,&continue JCN
		POP2 POP2 RTN
		&continue

	( keep x* ) OVR2 NIP 8MOD STH
	( get byte ) ,get-row JSR LDAk
	( mask ) #01 #07 STHr SUB SFL #ff EOR AND
	( save ) ROT ROT STA

RTN

@within-surface ( x* y* -- x* y* flag )

	( clamp ) DUP2 8// NIP .surface/h LDZ #01 SUB > ,&skip JCN
	( clamp ) OVR2 8// NIP .surface/w LDZ #01 SUB > ,&skip JCN
	#01 RTN
	&skip
		#00 RTN

RTN

@get-row ( x* y* -- row* )

	STH2k
		8// SWP2
		8// SWP2
		#00 .surface/w LDZ ** ++ 8**
	STH2r
		8MOD2 ++
	;data ++

RTN

( file )

@new-file ( path* -- )

	#00 ;draw-filepath JSR2
	( rename to untitled.txt )
	;filepath #0020 ;mclr JSR2
	;untitled-txt ;filepath #000c ;mcpy JSR2
	( erase data )
	;data GET-FRAME-SIZE ;mclr JSR2
	RESET-STATE
	( draw )
	;draw-viewport JSR2

RTN

@load-file ( -- )

	( erase data )
	;data GET-FRAME-SIZE ;mclr JSR2
	RESET-STATE
	;filepath ;scap JSR2 #0003 -- ;&tga-ext ;scmp JSR2 ;load-file-tga JCN2
	( load )
	;filepath .File/name DEO2
	#fff0 ;data -- .File/length DEO2
	;data .File/read DEO2
	( capture size )
	;filepath ;scap JSR2 #0009 -- ;read-size JSR2
	( draw )
	;draw-viewport JSR2

RTN
	&tga-ext "tga $1

@save-file ( path* -- )

	;filepath .File/name DEO2
	GET-FRAME-SIZE .File/length DEO2
	;data .File/write DEO2
	( draw )
	;draw-viewport JSR2

RTN

@read-size ( 00x00* -- )

	DUP2 ;sbyte JSR2 #00 = ,&cancel JCN
	DUP2 #0002 ++ LDA LIT 'x ! ,&cancel JCN
	DUP2 #0003 ++ ;sbyte JSR2 #00 = ,&cancel JCN
	( y ) DUP2 #0003 ++ ;sbyte JSR2 STH
	( x ) ;sbyte JSR2 STHr
		;set-surface-size JSR2

RTN
	&cancel POP2 RTN

( Drawing )

@draw-viewport ( -- )

	.viewport/size LDZ2 #0000 >> RTN?

	.viewport/x LDZ2 #0008 -- .viewport/y LDZ2 #0008 -- .viewport/w LDZ .viewport/h LDZ ;frame-inline-chrs ;draw-frame JSR2

	( titlebar )
	;draw-filestate JSR2
	#06 ;draw-filepath JSR2

@draw-surface ( -- )

	.surface/size LDZ2 #0000 >> RTN?

	.zoom/active LDZ ;draw-surface-zoom JCN2

	AUTO-XADDR
	;data .Screen/addr DEO2
	.surface/y LDZ2 .Screen/y DEO2
	.surface/h LDZ .viewport/h LDZ MAX #00
	&ver
		#00 OVR #00 .surface/w LDZ ** 8** ;data ++ .Screen/addr DEO2
		.surface/x LDZ2 .Screen/x DEO2
		.surface/w LDZ .viewport/w LDZ MAX #00
		&hor
			#09 .Screen/sprite DEO
			INC GTHk ,&hor JCN
		POP2
		.Screen/y DEI2k #0008 ++ ROT DEO2
		INC GTHk ,&ver JCN
	POP2

	;draw-filesize JSR2

	.surface/w LDZ .viewport/w LDZ #02 - > ,&no-handle JCN
	.surface/h LDZ .viewport/h LDZ #02 - > ,&no-handle JCN
	.surface/size LDZ2 #0000 == ,&no-handle JCN
		( draw surface handle )
		;surface-handle-icn .Screen/addr DEO2
		.surface-handle/x LDZ2 .Screen/x DEO2
		.surface-handle/y LDZ2 .Screen/y DEO2
		#0f .Screen/sprite DEO
		&no-handle

	( draw viewport handle )
	;viewport-handle-icn .Screen/addr DEO2
	.viewport/x2 LDZ2 #0008 -- .Screen/x DEO2
	.viewport/y2 LDZ2 #0008 -- .Screen/y DEO2
	#05 .Screen/sprite DEO

RTN

@draw-surface-zoom ( -- )

	AUTO-X
	.viewport/y LDZ2 .Screen/y DEO2
	.viewport/h LDZ #00
	&ver
		STHk
		.viewport/x LDZ2 .Screen/x DEO2
		.viewport/w LDZ #00
		&hor
			( get pixel )
			( y ) #00 STHkr .zoom/y LDZ2 ++ STH2
			( x ) #00 OVR .zoom/x LDZ2 ++ STH2r
			;get-pixel JSR2 TOS 8** ;bigpixel-icns ++ .Screen/addr DEO2
			#09 .Screen/sprite DEO
			INC GTHk ,&hor JCN
		POP2
		POPr
		.Screen/y DEI2 #0008 ++ .Screen/y DEO2
		INC GTHk ,&ver JCN
	POP2

RTN

@draw-tile ( x* y* -- )

	.zoom/active LDZ ,draw-tile-zoom JCN
	( bounds )
	DUP2 #00 .surface/h LDZ 8** DEC2 >> ,&cancel JCN
	OVR2 #00 .surface/w LDZ 8** DEC2 >> ,&cancel JCN
	( paint )
	#33 SFT2 SWP2 #33 SFT2 SWP2
	OVR2 OVR2 ;get-row JSR2 .Screen/addr DEO2
	.surface/y LDZ2 ++ .Screen/y DEO2
	.surface/x LDZ2 ++ .Screen/x DEO2
	#09 .Screen/sprite DEO

RTN
	&cancel POP2 POP2 RTN

@draw-tile-zoom ( x* y* -- )

	( bounds )
	DUP2 .zoom/y LDZ2 -- #00 .viewport/h LDZ DEC >> ,&cancel JCN
	OVR2 .zoom/x LDZ2 -- #00 .viewport/w LDZ DEC >> ,&cancel JCN
	( paint )
	OVR2 OVR2 ;get-pixel JSR2 TOS 8** ;bigpixel-icns ++ .Screen/addr DEO2
	.zoom/y LDZ2 -- 8** .viewport/y LDZ2 ++ .Screen/y DEO2
	.zoom/x LDZ2 -- 8** .viewport/x LDZ2 ++ .Screen/x DEO2
	#09 .Screen/sprite DEO

RTN
	&cancel POP2 POP2 RTN

@draw-cursor ( -- )

	( clear brush size )
	.cursor/x2 LDZ2
	.cursor/y2 LDZ2
	( clear size cursor )
	OVR2 OVR2
	#0003 -- .Screen/y DEO2
	#0003 -- .Screen/x DEO2
	#40 .Screen/sprite DEO
	( clear last cursor )
	#40 ;draw-pointer JSR2

	( record cursor positions )
	.Mouse/x DEI2 .cursor/x2 STZ2
	.Mouse/y DEI2 .cursor/y2 STZ2

	( draw size cursor )
	.brush/tool LDZ DUP #01 = SWP #03 = #0000 == ;&outside-canvas JCN2
	( outside canvas )
	.Mouse/x DEI2 .Mouse/y DEI2 .surface ;within-rect JSR2 #00 = ;&outside-canvas JCN2
		.cursor/x2 LDZ2 #0003 -- .Screen/x DEO2
		.cursor/y2 LDZ2 #0003 -- .Screen/y DEO2
		;brush-icns #00 .brush/size LDZ 8** ++ .Screen/addr DEO2
		.Mouse/state DEI #00 ! DUP 2* #41 ADD .Screen/sprite DEO
		#00 = ,&outside-canvas JCN RTN
	&outside-canvas

	( draw new cursor )
	.cursor/x2 LDZ2
	.cursor/y2 LDZ2
	#4143 .Mouse/state DEI #01 JCN SWP POP
		,draw-pointer JSR

RTN

@draw-pointer ( x* y* color -- )

	STH
	.Screen/y DEO2
	.Screen/x DEO2
	;pointers-icns .Screen/addr DEO2
	STHr .Screen/sprite DEO

RTN

@clear-panel ( -- )

	.panel/x LDZ2 .Screen/x DEO2
	.panel/y LDZ2 .Screen/y DEO2
	#03 #11 #00 ;fill-rect JSR2

RTN

@clear-viewport ( -- )

	.viewport-label/x LDZ2 #0002 -- .Screen/x DEO2
	.viewport-label/y LDZ2 #0002 -- .Screen/y DEO2
	.viewport/w LDZ INC .viewport/h LDZ #02 + #00 ;fill-rect JSR2

RTN

@draw-panel ( -- )

	( frame )
	.panel/x LDZ2 .panel/y LDZ2 #01 #0f ;frame-outline-chrs ;draw-frame JSR2
	( tools )
	,draw-toolpane JSR
	.panel/x LDZ2 INC2 .panel/x2 LDZ2 .panel/y LDZ2 #0026 ++ #01 ,line-hor JSR
	,draw-pattpane JSR
	.panel/x LDZ2 INC2 .panel/x2 LDZ2 .panel/y LDZ2 #0058 ++ #01 ,line-hor JSR
	,draw-sizepane JSR

RTN

@line-hor ( x0* x1* y* color -- )

	STH .Screen/y DEO2 SWP2
	&loop
		( save ) DUP2 .Screen/x DEO2
		( draw ) STHkr .Screen/pixel DEO
		INC2 INC2 GTH2k ,&loop JCN
	POP2 POP2 POPr

RTN

@draw-toolpane ( -- )

	;tool-icns .Screen/addr DEO2
	#06 .brush/tool LDZ
	.panel/x LDZ2 #0004 ++
	.panel/y LDZ2 #0005 ++
		,draw-icons JSR

RTN

@draw-pattpane ( -- )

	;patt-icns .Screen/addr DEO2
	#08 .brush/patt LDZ
	.panel/x LDZ2 #0004 ++
	.panel/y LDZ2 #002d ++
		,draw-icons JSR

RTN

@draw-sizepane ( -- )

	#00 .brush/mode LDZ 40* ;size-icns ++ .Screen/addr DEO2
	#08 .brush/size LDZ
	.panel/x LDZ2 #0004 ++
	.panel/y LDZ2 #005f ++
		,draw-icons JSR

RTN

@draw-icons ( length selection x* y* -- )

	AUTO-ADDR
	,&anchor-y STR2
	,&anchor-x STR2
	STH #00
	&loop
		#00 OVR 2MOD #09 * [ LIT2 &anchor-x $2 ] ++ .Screen/x DEO2
		#00 OVR 2/ #0a * [ LIT2 &anchor-y $2 ] ++ .Screen/y DEO2
		DUP STHkr = 2* #09 + .Screen/sprite DEO
		INC GTHk ,&loop JCN
	POP2
	POPr

RTN

@draw-filestate ( -- )

	AUTO-ADDR
	.viewport-label/x LDZ2 .Screen/x DEO2
	.viewport-label/y LDZ2 #0002 -- .Screen/y DEO2
	;save-icns .Screen/addr DEO2
	#01 .Screen/sprite DEO
	#0a .state/changed LDZ #05 * + .Screen/sprite DEO

RTN

@draw-filepath ( color -- )

	STH
	.viewport-label/x LDZ2 #0008 ++ .Screen/x DEO2
	.viewport-label/y LDZ2 #0002 -- .Screen/y DEO2
	;filepath STHkr ;draw-str JSR2
	( cap )
	;save-icns/cap .Screen/addr DEO2
	STHr #00 ! .Screen/sprite DEO

RTN

@draw-filesize ( -- )

	.zoom/active LDZ #00 = #01 JCN RTN
	.viewport/w LDZ #05 > #01 JCN RTN
	.surface/y LDZ2 .viewport/y LDZ2 >> #01 JCN RTN
	
	AUTO-X
	.surface/x LDZ2 .Screen/x DEO2
	.surface/y LDZ2 #0008 -- .Screen/y DEO2
	.surface/w LDZ #08 ;draw-byte JSR2
	LIT 'x #08 ;draw-char JSR2
	.surface/h LDZ #08 ;draw-byte JSR2

RTN

( strings )

@draw-str ( text* color -- )

	STH
	AUTO-X
	&while
		LDAk STHkr ,draw-char JSR
		INC2 LDAk ,&while JCN
	POP2
	POPr

RTN

@draw-byte ( byte color -- )

	STH DUP #04 SFT STHkr ,draw-hex JSR STHr

@draw-hex ( char color -- )

	SWP #0f AND [ DUP #09 > #27 * + #30 + ] SWP

@draw-char ( char color -- )

	SWP #20 - TOS 8** ;font ++ .Screen/addr DEO2
	.Screen/sprite DEO

RTN

( ux )

@within-rect ( x* y* rect -- flag )

	STH
	( y < rect.y1 ) DUP2 STHkr #02 ADD LDZ2 LTS2 ,&skip JCN
	( y > rect.y2 ) DUP2 STHkr #06 ADD LDZ2 GTS2 ,&skip JCN
	SWP2
	( x < rect.x1 ) DUP2 STHkr LDZ2 LTS2 ,&skip JCN
	( x > rect.x2 ) DUP2 STHkr #04 ADD LDZ2 GTS2 ,&skip JCN
	POP2 POP2 POPr
	#01
RTN
	&skip
	POP2 POP2 POPr
	#00

RTN

@line-diag ( rect color -- )

	( color ) STH
	( x0 ) LDZ2k #0001 SUB2 .line-guide/x2 STZ2
	( y0 ) INC INC LDZ2k #0001 SUB2  .line-guide/y2 STZ2
	( x1 ) INC INC LDZ2k .line-guide/x1 STZ2
	( y1 ) INC INC LDZ2 .line-guide/y1 STZ2
	.line-guide/x1 LDZ2 .line-guide/x2 LDZ2 -- ABS2 .line-guide/dx STZ2
	.line-guide/y1 LDZ2 .line-guide/y2 LDZ2 -- ABS2 #0000 SWP2 -- .line-guide/dy STZ2
	#ffff #00 .line-guide/x2 LDZ2 .line-guide/x1 LDZ2 LTS2 2** ++ .line-guide/sx STZ2
	#ffff #00 .line-guide/y2 LDZ2 .line-guide/y1 LDZ2 LTS2 2** ++ .line-guide/sy STZ2
	.line-guide/dx LDZ2 .line-guide/dy LDZ2 ++ .line-guide/e1 STZ2
	&loop
		( paint ) .line-guide/x2 LDZ2 .Screen/x DEO2 .line-guide/y2 LDZ2 .Screen/y DEO2 STHkr .Screen/pixel DEO
		.line-guide/x2 LDZ2 .line-guide/x1 LDZ2 == .line-guide/y2 LDZ2 .line-guide/y1 LDZ2 == AND ,&end JCN
		.line-guide/e1 LDZ2 2** .line-guide/e2 STZ2
		.line-guide/e2 LDZ2 .line-guide/dy LDZ2 LTS2 ,&skipy JCN
			.line-guide/e1 LDZ2 .line-guide/dy LDZ2 ++ .line-guide/e1 STZ2
			.line-guide/x2 LDZ2 .line-guide/sx LDZ2 ++ .line-guide/x2 STZ2
			&skipy
		.line-guide/e2 LDZ2 .line-guide/dx LDZ2 GTS2 ,&skipx JCN
			.line-guide/e1 LDZ2 .line-guide/dx LDZ2 ++ .line-guide/e1 STZ2
			.line-guide/y2 LDZ2 .line-guide/sy LDZ2 ++ .line-guide/y2 STZ2
			&skipx
		;&loop JMP2
	&end
	POPr

RTN

@guide-rect ( color -- )

	STH
	.guide/x2 LDZ2 .guide/x LDZ2 ( flip sign ) GTH2k JMP SWP2
	&hor
		DUP2 .Screen/x DEO2
		.guide/y LDZ2 .Screen/y DEO2 STHkr .Screen/pixel DEO
		.guide/y2 LDZ2 .Screen/y DEO2 STHkr .Screen/pixel DEO
		INC2 GTH2k ,&hor JCN
	POP2 POP2
	.guide/y2 LDZ2 .guide/y LDZ2 ( flip sign ) GTH2k JMP SWP2
	&ver
		DUP2 .Screen/y DEO2
		.guide/x LDZ2 .Screen/x DEO2 STHkr .Screen/pixel DEO
		.guide/x2 LDZ2 .Screen/x DEO2 STHkr .Screen/pixel DEO
		INC2 GTH2k ,&ver JCN
	POP2 POP2
	POPr

RTN

@snarf-txt ".snarf $1

( Tools )

@tools
	:pencil/hover  :pencil/up  :pencil/down  :pencil/drag
	:marker/hover  :marker/up  :marker/down  :marker/drag
	:stroke/hover  :stroke/up  :stroke/down  :stroke/drag
	:magic/hover   :magic/up   :magic/down   :magic/drag
	:box/hover     :box/up     :box/down     :box/drag
	:magnify/hover :magnify/up :magnify/down :magnify/drag

@pencil ( x* y* -> )

	&down
		OVR2 OVR2 .cursor/y1 STZ2 .cursor/x1 STZ2
		OVR2 OVR2 .brush/oper LDZ2 JSR2
	&up
		;draw-tile JSR2
		SET-STATE
		BRK
	&drag
		OVR2 OVR2 STH2 STH2
		.cursor/x1 LDZ2 .cursor/y1 LDZ2 STH2r STH2r
		;paint-line JSR2
		.cursor/y1 STZ2 .cursor/x1 STZ2
		BRK
	&hover
		POP2 POP2

BRK

@marker ( x* y* -> )

	&up
		POP2 POP2
		SET-STATE BRK
	&down
	&drag
		#0003 -- SWP2 #0003 -- SWP2
		;paint-brush JSR2
		BRK
	&hover
		POP2 POP2

BRK

@stroke ( x* y* -> )

	&down
		( clear cursor ) .cursor/x2 LDZ2 .cursor/y2 LDZ2 #40 ;draw-pointer JSR2
		( erase guide ) .guide #40 ;line-diag JSR2
		.cursor/y1 STZ2 .cursor/x1 STZ2
		BRK
	&up
		( clear cursor ) .cursor/x2 LDZ2 .cursor/y2 LDZ2 #40 ;draw-pointer JSR2
		( erase guide ) .guide #40 ;line-diag JSR2
		.cursor/y2 STZ2 .cursor/x2 STZ2
		.cursor/x1 LDZ2 .cursor/y1 LDZ2 .cursor/x2 LDZ2 .cursor/y2 LDZ2 ;paint-line JSR2
		SET-STATE
		BRK
	&drag
		( clear cursor ) .cursor/x2 LDZ2 .cursor/y2 LDZ2 #40 ;draw-pointer JSR2
		( erase guide ) .guide #40 ;line-diag JSR2
		.cursor/y2 STZ2 .cursor/x2 STZ2
		( update guide )
		.cursor/x1 LDZ2 .cursor/y1 LDZ2 ;to-screen JSR2 INC2 .guide/y STZ2 INC2 .guide/x STZ2
		.cursor/x2 LDZ2 .cursor/y2 LDZ2 ;to-screen JSR2 .guide/y2 STZ2 .guide/x2 STZ2
		( erase guide ) .guide #43 ;line-diag JSR2
		BRK
	&hover
		POP2 POP2

BRK

@magic ( x* y* -> )

	&up
		SET-STATE
	&down
	&drag
		#0003 -- SWP2 #0003 -- SWP2
		;paint-magic JSR2
		BRK
	&hover
		POP2 POP2

BRK

@box ( x* y* -> )

	&down
		( clear cursor ) .cursor/x2 LDZ2 .cursor/y2 LDZ2 #40 ;draw-pointer JSR2
		( erase guide ) #40 ;guide-rect JSR2
		.cursor/y1 STZ2 .cursor/x1 STZ2
		BRK
	&up
		( clear cursor ) .cursor/x2 LDZ2 .cursor/y2 LDZ2 #40 ;draw-pointer JSR2
		( erase guide ) #40 ;guide-rect JSR2
		.cursor/y2 STZ2 .cursor/x2 STZ2
		;paint-rect JSR2
		SET-STATE
		BRK
	&drag
		( clear cursor ) .cursor/x2 LDZ2 .cursor/y2 LDZ2 #40 ;draw-pointer JSR2
		( erase guide ) #40 ;guide-rect JSR2
		.cursor/y2 STZ2 .cursor/x2 STZ2
		( update guide )
		.cursor/x1 LDZ2 .cursor/y1 LDZ2 ,to-screen JSR .guide/y STZ2 .guide/x STZ2
		.cursor/x2 LDZ2 .cursor/y2 LDZ2 ,to-screen JSR .guide/y2 STZ2 .guide/x2 STZ2
		( draw guide ) #43 ;guide-rect JSR2
		BRK
	&hover
		POP2 POP2

BRK

@to-screen ( x* y* -- x* y* )

	.zoom/active LDZ ,&on-zoom JCN
		.surface/y LDZ2 ++ SWP2
		.surface/x LDZ2 ++ SWP2
	RTN

	&on-zoom
		.zoom/y LDZ2 -- 8** .viewport/y LDZ2 ++ SWP2
		.zoom/x LDZ2 -- 8** .viewport/x LDZ2 ++ SWP2

RTN

@magnify ( x* y* -> )

	&down
		#00 .surface/h LDZ 2// -- .zoom/y STZ2
		#00 .surface/w LDZ 2// -- .zoom/x STZ2
		;toggle-zoom JSR2
		BRK
	&up
	&drag
	&hover
		POP2 POP2

BRK

@untitled-txt  "untitled.icn $1

~src/stdlib.tal
~src/tga.tal
~src/gly.tal
~src/assets.tal

@data
