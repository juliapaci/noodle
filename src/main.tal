(
	app/noodle : illustration program

	right-click or escape - erase
	arrows - move zoom
	alt+arrows - resize canvas
	space or shift - toogle zoom
	tab - invert image
	enter - next frame
	backspace - blank canvas
	brackets - set brush size
	1-8 - select pattern
	ctrl+n - clear
	ctrl+r - rename
	ctrl+o - load
	ctrl+s - save
	ctrl+p - snarf glyph
	q - pen tool
	w - brush tool
	e - line tool
	r - magic tool
	t - rectangle tool
	y - zoom tool
)

~src/utils.tal

%2++  { INC2 INC2 }

%SWP? { #01 JCN SWP }
%SWP2? { #01 JCN SWP2 }
%BRK? { #01 JCN BRK }
%RTN? { #01 JCN RTN }
%DEC2 { #0001 -- }
%SFL  { #40 SFT SFT }
%ABS2 { DUP2 #0f SFT2 = #05 JCN #0000 SWP2 -- }
%LTS2 { #8000 ++ SWP2 #8000 ++ >> }
%GTS2 { #8000 ++ SWP2 #8000 ++ << }

%PAD { #0003 }

%GET-ITER { OVR2 NIP OVR SWP }
%ADD-POSITIONS { ROT2 ++ ROT2 ROT2 ++ SWP2 }

%SET-STATE { #01 .state/changed STZ ;draw-filepane/state JSR2 }
%RESET-STATE { #00 .state/changed STZ ;draw-filepane/state JSR2 }

%GET-FRAME-SIZE { .canvas/h LDZ2 .canvas/w LDZ2 MUL2 8** } ( -- length* )
%GET-ANIM-SIZE { GET-FRAME-SIZE .anim/limit LDZ INC TOS MUL2 } ( -- length* )
%GET-FRAME-ADDR { TOS GET-FRAME-SIZE ** ;data ++ } ( frame -- addr* )

%SIZE-TO-RECT {
	STH2 STH2 OVR2 STH2r ++ OVR2 STH2r ++
} ( x y w h -- x1 y1 x2 y2 )

( devices )

|00 @System     &vector $2 &pad     $6 &r      $2 &g     $2 &b     $2
|10 @Console    &vector $2 &read    $1 &pad    $5 &write $1
|20 @Screen     &vector $2 &width   $2 &height $2 &auto  $1 &pad   $1 &x     $2 &y $2 &addr $2 &pixel $1 &sprite $1
|80 @Controller &vector $2 &button  $1 &key    $1
|90 @Mouse      &vector $2 &x       $2 &y      $2 &state $1 &chord $1
|a0 @File       [ &vector $2 &success  $2 &stat   $2 &delete $1 &append $1 &name  $2 &length $2 &read $2 &write $2 ]

( variables )

|0000

@state
	&timer $1 &blink $1 &presentation $1 &changed $1
@path
	&name $20
@brush
	&tool $1 &size $1 &patt $1 &oper $2 &mode $1
@cursor
	&x1 $2 &y1 $2
	&x2 $2 &y2 $2
	&last $1
@zoom
	&active $1
	&x $2 &y $2
@anim
	&frame $1 &limit $1 &onion $1 &addr $2
@theme
	&r $1 &g $1 &b $1
@center
	&x $2 &y $2
@canvas
	&x1 $2 &y1 $2 &x2 $2 &y2 $2
	&w $1 &w-short $1 &h $1 &h-short $1
@guide
	&x1 $2 &y1 $2 &x2 $2 &y2 $2
@toolpane
	&x1 $2 &y1 $2 &x2 $2 &y2 $2
@pattpane
	&x1 $2 &y1 $2 &x2 $2 &y2 $2
@sizepane
	&x1 $2 &y1 $2 &x2 $2 &y2 $2
@animpane
	&x1 $2 &y1 $2 &x2 $2 &y2 $2
@filepane
	&x1 $2 &y1 $2 &x2 $2 &y2 $2
@namepane
	&x1 $2 &y1 $2 &x2 $2 &y2 $2
@line
	&x1 $2 &y1 $2 &x2 $2 &y2 $2 &sx $2 &sy $2 &dx $2 &dy $2 &e1 $2 &e2 $2
@line-guide
	&x1 $2 &y1 $2 &x2 $2 &y2 $2 &sx $2 &sy $2 &dx $2 &dy $2 &e1 $2 &e2 $2 &color $1

( program )

|0100

	( vectors )
	;on-console .Console/vector DEO2
	;untrap JSR2

	( colors )
	#70fe .System/r DEO2
	#c0f3 .System/g DEO2
	#90f3 .System/b DEO2

	( default brush )
	#04 .brush/size STZ
	#00 .brush/patt STZ
	#00 .brush/tool STZ

	.Screen/width DEI2 2// .center/x STZ2
	.Screen/height DEI2 2// .center/y STZ2

	( above panes )
	#0010 #000c #0030 PAD ++ #0008 PAD ++ SIZE-TO-RECT
		.toolpane/y2 STZ2 .toolpane/x2 STZ2 .toolpane/y1 STZ2 .toolpane/x1 STZ2
	#0048 #000c #0040 PAD ++ #0008 PAD ++ SIZE-TO-RECT
		.sizepane/y2 STZ2 .sizepane/x2 STZ2 .sizepane/y1 STZ2 .sizepane/x1 STZ2
	.Screen/width DEI2 #0053 -- #000c #0040 PAD ++ #0008 PAD ++ SIZE-TO-RECT
		.pattpane/y2 STZ2 .pattpane/x2 STZ2 .pattpane/y1 STZ2 .pattpane/x1 STZ2

	( below panes )
	#0010 .Screen/height DEI2 #0010 -- #0020 #0008 SIZE-TO-RECT
		.animpane/y2 STZ2 .animpane/x2 STZ2 .animpane/y1 STZ2 .animpane/x1 STZ2
	.Screen/width DEI2 #0031 -- .Screen/height DEI2 #0010 -- #0020 #0008 SIZE-TO-RECT
		.filepane/y2 STZ2 .filepane/x2 STZ2 .filepane/y1 STZ2 .filepane/x1 STZ2
	#0038 .Screen/height DEI2 #0010 -- .Screen/width DEI2 #0070 -- #0008 SIZE-TO-RECT
		.namepane/y2 STZ2 .namepane/x2 STZ2 .namepane/y1 STZ2 .namepane/x1 STZ2

	( starting frame )
	.anim/frame LDZ GET-FRAME-ADDR .anim/addr STZ2
		#01 .anim/onion STZ

	( default size )
	.Screen/width DEI2 8// #0004 --
	.Screen/height DEI2 8// #0006 --
		;set-size JSR2

	( ready. )
	;clear JSR2
	;draw-filepane/state JSR2

	( load default file )
	;new-file JSR2

	( theme support )
	;load-theme JSR2

BRK

@untrap ( -- )

	( vectors )
	;on-frame .Screen/vector DEO2
	;on-button .Controller/vector DEO2
	;on-mouse .Mouse/vector DEO2

	( filepath )
	#00 .state/blink STZ
	#01 ;draw-filepath JSR2
	RELEASE-MOUSE

RTN

@trap ( -- )

	( vectors )
	;on-frame-trap .Screen/vector DEO2
	;on-button-trap .Controller/vector DEO2
	;on-mouse-trap .Mouse/vector DEO2

	( filepath )
	#01 .state/blink STZ
	#01 ;draw-filepath JSR2
	RELEASE-MOUSE

	( clear brush )
	.cursor/x2 LDZ2 .Screen/x DEO2
	.cursor/y2 LDZ2 .Screen/y DEO2
	#40 .Screen/sprite DEO

RTN

@on-console ( -> )

	( starting )
	;&trap LDA ,&started JCN
		#00 ;draw-filepath JSR2
		;path/name #0020 ;mclr JSR2
		#01 ,&trap STR
		&started
	( append )
	.Console/read DEI #20 < ,&validate JCN
	;path/name ;slen JSR2 #0020 == ,&validate JCN
		;path/name .Console/read DEI ;sput JSR2 
		BRK
		&validate
	( validate )
	;load-file JSR2
	#01 ;draw-filepath JSR2
	#00 ,&trap STR

BRK
	&trap $1

@on-frame ( -> )

	( inactive )

BRK

@on-frame-trap ( -> )

	.state/timer LDZ INC
	DUP 8MOD ,&no-blink JCN
		.state/blink LDZ #00 =
			DUP 8* INC ;draw-filepath JSR2
			.state/blink STZ
		&no-blink
	.state/timer STZ

BRK

@on-mouse-trap ( -> )

	( release trap on touch )
	.Mouse/state DEI BRK?

	;untrap JSR2

BRK

@on-button-trap ( -> )

	.Controller/key DEI
	DUP ,&continue JCN
		POP BRK
		&continue
	( backspace ) #08 !~ ,&no-edit-backspace JCN
		( clamp ) ;path/name ;slen JSR2 NIP #00 = ,&edit-end JCN
		#00 ;draw-filepath JSR2
		;path/name ;spop JSR2
		#01 ;draw-filepath JSR2
		POP BRK
		&no-edit-backspace
	( special ) DUP IS-VALID ,&no-edit-enter JCN
		;untrap JSR2
		POP BRK
		&no-edit-enter
	( append ) ;path/name ;slen JSR2 NIP #1f = ,&edit-end JCN
	DUP ;path/name ROT ;sput JSR2
	#01 ;draw-filepath JSR2
	&edit-end
	POP

BRK

@on-mouse ( -> )

	;draw-cursor JSR2

	.Mouse/x DEI2 .Mouse/y DEI2

	( begin with canvas )
	OVR2 OVR2 .canvas ;within-rect JSR2
		;on-touch-canvas JCN2

	( only when click originates in pane )
	.Mouse/state DEI .cursor/last LDZ #0100 !! ,&end JCN

	( anim enabled in presentation )
	OVR2 OVR2 .animpane ;within-rect JSR2
		;on-touch-animpane JCN2

	( disable with presentation )
	.state/presentation LDZ ,&end-present JCN

	OVR2 OVR2 .toolpane ;within-rect JSR2
		;on-touch-toolpane JCN2
	OVR2 OVR2 .sizepane ;within-rect JSR2
		;on-touch-sizepane JCN2
	OVR2 OVR2 .pattpane ;within-rect JSR2
		;on-touch-pattpane JCN2
	OVR2 OVR2 .filepane ;within-rect JSR2
		;on-touch-filepane JCN2
	OVR2 OVR2 .namepane ;within-rect JSR2
		;on-touch-namepane JCN2

	&end-present

	( toggle present mode )
	;present JSR2

	&end
	POP2 POP2

BRK

@on-touch-canvas ( x* y* -> )

	( set erase from right-click/mode )
	[ ;remove-pixel ;add-pixel ] .Mouse/state DEI #01 > .brush/mode LDZ + SWP2? POP2 .brush/oper STZ2
	( get mouse state )
	.Mouse/state DEI #00 > #10 SFT .cursor/last LDZ #00 > + #00 SWP 2**
	( non hover states )
	DUP2 #0000 >> ,&continue JCN
		POP2 POP2 POP2 BRK
		&continue
	( get tool offset )
	.brush/tool LDZ #00 SWP 8**
	( record state )
	.Mouse/state DEI .cursor/last STZ
	( find fns )
	;tools ++ ++ LDA2 STH2
	( canvas offset )
	.canvas/y1 LDZ2 -- SWP2
	.canvas/x1 LDZ2 -- SWP2
	( zoom offset )
	.zoom/active LDZ #01 ! ,&no-zoom JCN
		8// .zoom/y LDZ2 ++ SWP2
		8// .zoom/x LDZ2 ++ SWP2
		&no-zoom
	STH2r JMP2

BRK

@on-touch-toolpane ( x* y* -- )

	( unused y ) POP2

	.toolpane/x1 LDZ2 -- 8// NIP

	DUP #06 < ,&valid JCN
		POP BRK
		&valid

	;select-tool JSR2
	RELEASE-MOUSE

BRK

@on-touch-sizepane ( x* y* -- )

	( unused y ) POP2

	.sizepane/x1 LDZ2 -- 8// NIP
		;select-size JSR2
	RELEASE-MOUSE

BRK

@on-touch-pattpane ( x* y* -- )

	( unused y ) POP2

	.pattpane/x1 LDZ2 -- 8// NIP

	DUP #08 < ,&valid JCN
		POP BRK
		&valid

	;select-patt JSR2
	RELEASE-MOUSE

BRK

@on-touch-animpane ( x* y* -- )

	( unused y ) POP2

	.animpane/x1 LDZ2 -- 8// NIP

	[ #00 ] !~ ,&no-onion JCN
		.anim/onion TOGGLE
		;redraw JSR2
		RELEASE-MOUSE POP BRK
		&no-onion
	[ #02 ] !~ ,&no-next JCN
		;next-frame JSR2
		;redraw JSR2
		RELEASE-MOUSE POP BRK
		&no-next
	[ #03 ] !~ ,&no-frame JCN
		.Mouse/state DEI #00 ! ;mod-limit JSR2
		RELEASE-MOUSE POP BRK
		&no-frame
	POP

BRK

@on-touch-filepane ( x* y* -- )

	( unused y ) POP2

	.filepane/x1 LDZ2 -- 8// NIP

	[ #00 ] !~ ,&no-blank JCN
		;new-file JSR2
		;draw-canvas JSR2
		RELEASE-MOUSE POP BRK
		&no-blank
	[ #01 ] !~ ,&no-load JCN
		;load-file JSR2
		;draw-canvas JSR2
		RELEASE-MOUSE POP BRK
		&no-load
	[ #03 ] !~ ,&no-save JCN
		;path/name ;save-file JSR2
		RELEASE-MOUSE POP BRK
		&no-save
	POP

BRK

@on-touch-namepane ( x* y* -- )

	( unused x y ) POP2 POP2

	#00 ;draw-filepath JSR2
	;trap JSR2

BRK

@on-button ( -> )

	.Controller/button DEI
		DUP #01 AND ;on-control JCN2
		DUP #02 AND ;on-alt JCN2
		DUP #04 ! ,&no-shift JCN
			.zoom/active TOGGLE
			;redraw JSR2
			&no-shift
		DUP #08 ! ,&no-esc JCN
			.brush/mode TOGGLE
			;draw-toolpane JSR2
			&no-esc
	POP

	( control zoom )
	.zoom/active LDZ #00 = ,&skip-zoom JCN
		.Controller/button DEI #f0 AND
		DUP #04 SFT #01 AND #01 ! ,&no-up JCN
			( move ) .zoom/y LDZ2 #0001 -- .zoom/y STZ2
			&no-up
		DUP #05 SFT #01 AND #01 ! ,&no-down JCN
			( move ) .zoom/y LDZ2 INC2 .zoom/y STZ2
			&no-down
		DUP #06 SFT #01 AND #01 ! ,&no-left JCN
			( move ) .zoom/x LDZ2 #0001 -- .zoom/x STZ2
			&no-left
		DUP #07 SFT #01 AND #01 ! ,&no-right JCN
			( move ) .zoom/x LDZ2 INC2 .zoom/x STZ2
			&no-right
		#00 = #04 JCN ;draw-canvas JSR2
	&skip-zoom

	.Controller/key DEI
	DUP #00 ! ,&continue JCN
		POP BRK &continue
	DUP #1b ! ,&no-escape JCN
		;load-theme JSR2 POP BRK &no-escape
	DUP #08 ! ,&no-backspace JCN
		( erase ) POP ;clear JSR2 BRK &no-backspace
	DUP #0d ! ,&no-enter JCN
		POP ;next-frame JSR2 ;redraw JSR2 BRK &no-enter
	DUP #09 ! ,&no-invert JCN
		( invert ) POP ;invert JSR2 BRK &no-invert
	DUP #20 ! ,&no-space JCN
		( toggle zoom ) .zoom/active TOGGLE ;redraw JSR2 &no-space
	DUP LIT 'q ! ,&no-qkey JCN
		( tool0 ) #00 ;select-tool JSR2 &no-qkey
	DUP LIT 'w ! ,&no-wkey JCN
		( tool1 ) #01 ;select-tool JSR2 &no-wkey
	DUP LIT 'e ! ,&no-ekey JCN
		( tool2 ) #02 ;select-tool JSR2 &no-ekey
	DUP LIT 'r ! ,&no-rkey JCN
		( tool3 ) #03 ;select-tool JSR2 &no-rkey
	DUP LIT 't ! ,&no-tkey JCN
		( tool4 ) #04 ;select-tool JSR2 &no-tkey
	DUP LIT 'y ! ,&no-ykey JCN
		( tool5 ) #05 ;select-tool JSR2 &no-ykey
	DUP LIT '[ ! ,&no-lbracket JCN
		.brush/size LDZ #01 - ;select-size JSR2 &no-lbracket
	DUP LIT '] ! ,&no-rbracket JCN
		.brush/size LDZ INC ;select-size JSR2 &no-rbracket
	DUP
		DUP #30 > SWP #39 < #0101 !! ,&no-numkey JCN
		( size ) DUP #31 SUB ;select-patt JSR2 &no-numkey
	POP

BRK

@on-control ( button -> )

	POP
	.Controller/key DEI
	DUP [ LIT 'r ] ! ,&no-name JCN
		#00 ;draw-filepath JSR2
		;trap JSR2
		POP BRK
		&no-name
	DUP [ LIT 'R ] ! ,&no-rename JCN
		#00 ;draw-filepath JSR2
		;path/name #0020 ;mclr JSR2
		;trap JSR2
		POP BRK
		&no-rename
	DUP [ LIT 'n ] ! ,&no-new JCN
		#25 ;draw-filepath JSR2
		;new-file JSR2
		;redraw JSR2 POP BRK &no-new
	DUP [ LIT 'o ] ! ,&no-open JCN
		;load-file JSR2
		;redraw JSR2 POP BRK &no-open
	DUP [ LIT 's ] ! ,&no-save JCN
		;path/name ;save-file JSR2
		;draw-filepane/state JSR2 POP BRK  &no-save
	DUP [ LIT 'p ] ! ,&no-copy JCN
		;glyph-snarf JSR2 POP BRK  &no-copy
	POP

BRK

@on-alt ( button -> )

	#04 SFT
	[ #01 !~ ] ,&no-up JCN
		.canvas/w LDZ2 .canvas/h LDZ2 #0001 -- ;set-size JSR2
		&no-up
	[ #02 !~ ] ,&no-down JCN
		.canvas/w LDZ2 .canvas/h LDZ2 INC2 ;set-size JSR2
		&no-down
	[ #04 !~ ] ,&no-left JCN
		.canvas/w LDZ2 #0001 -- .canvas/h LDZ2 ;set-size JSR2
		&no-left
	[ #08 !~ ] ,&no-right JCN
		.canvas/w LDZ2 INC2 .canvas/h LDZ2 ;set-size JSR2
		&no-right
	POP

BRK

@select-tool ( tool -- )

	.brush/tool STZ
	;draw-toolpane JSR2

RTN

@select-patt ( patt -- )

	.brush/patt STZ
	( select brush )
	.brush/tool LDZ ,&no-switch JCN
		#01 ;select-tool JSR2
		&no-switch
	;draw-pattpane JSR2

RTN

@select-size ( size -- )

	8MOD
	( toggle mode on reselection )
	DUP .brush/size LDZ ! ,&no-toggle JCN
		.brush/mode TOGGLE
		&no-toggle
	.brush/size STZ
	( select brush )
	.brush/tool LDZ ,&no-switch JCN
		#01 ;select-tool JSR2
		&no-switch
	;draw-sizepane JSR2

RTN

@prev-frame ( -- )

	.anim/frame LDZ ,&not-zero JCN
		.anim/limit LDZ
		DUP GET-FRAME-ADDR .anim/addr STZ2
		.anim/frame STZ
		RTN
		&not-zero
	.anim/frame LDZ #01 -
		DUP GET-FRAME-ADDR .anim/addr STZ2
		.anim/frame STZ

RTN

@next-frame ( -- )

	.anim/frame LDZ .anim/limit LDZ ! ,&not-limit JCN
		#00
		DUP GET-FRAME-ADDR .anim/addr STZ2
		.anim/frame STZ
		RTN
		&not-limit
	.anim/frame LDZ INC
		DUP GET-FRAME-ADDR .anim/addr STZ2
		.anim/frame STZ

RTN

@clear ( -- )

	.anim/addr LDZ2 STH2k GET-FRAME-SIZE ++ STH2r
	&loop
		( save ) DUP2 #00 ROT ROT STA
		INC2 GTH2k ,&loop JCN
	POP2 POP2
	;redraw JSR2

RTN

@invert ( -- )

	.anim/addr LDZ2 STH2k GET-FRAME-SIZE ++ STH2r
	&loop
		( load ) DUP2 DUP2 LDA #ff EOR
		( save ) ROT ROT STA
		INC2 GTH2k ,&loop JCN
	POP2 POP2
	;redraw JSR2

RTN

@set-size ( w* h* -- )

		DUP2 .canvas/h LDZ2 !! ,&changed JCN
		OVR2 .canvas/w LDZ2 !! ,&changed JCN
		POP2 POP2 RTN
	&changed

	( undraw )
	.canvas #00 ;fill-rect JSR2

	( clamp )
	[ #0002 ] MIN2 [ .Screen/height DEI2 8// #0006 -- ] MAX2
		STH2k .canvas/h STZ2
	[ #0002 ] MIN2 [ .Screen/width DEI2 8// #0004 -- ] MAX2
		STH2k .canvas/w STZ2

	.center/x LDZ2 STH2kr 8** 2// -- .canvas/x1 STZ2
	STH2r 8** .canvas/x1 LDZ2 ++ .canvas/x2 STZ2
	.center/y LDZ2 STH2kr 8** 2// -- .canvas/y1 STZ2
	STH2r 8** .canvas/y1 LDZ2 ++ .canvas/y2 STZ2

	( draw ui )
	;draw-background JSR2

	( draw size )
	.Screen/width DEI2 #0078 -- .Screen/x DEO2
	#000d .Screen/y DEO2
	.canvas/w LDZ2 #80 SFT2 .canvas/h LDZ2 ++ #01 ;print/short JSR2

	( frame )
	.canvas #01 ;line-rect JSR2

	;draw-canvas JSR2

RTN

@present ( -- )

	.state/presentation TOGGLE

	.state/presentation LDZ ,&hide JCN
	,&show JMP

	&show
		( load theme )
		.theme/r LDZ .System/r DEO
		.theme/g LDZ .System/g DEO
		.theme/b LDZ .System/b DEO
		;draw-background JSR2
		;draw-canvas-outside JSR2
		RELEASE-MOUSE
		RTN

	&hide
		( save theme )
		.System/r DEI DUP .theme/r STZ #0f AND .System/r DEO
		.System/g DEI DUP .theme/g STZ #0f AND .System/g DEO
		.System/b DEI DUP .theme/b STZ #0f AND .System/b DEO
		( clear panes )
		.toolpane #00 ;fill-rect JSR2
		.sizepane #00 ;fill-rect JSR2
		.pattpane #00 ;fill-rect JSR2
		;draw-background JSR2
		;draw-canvas-outside JSR2
		RELEASE-MOUSE
		RTN

RTN

@mod-limit ( mod -- )

	( reset ) #00 .anim/frame STZ
	.anim/limit LDZ + DUP #07 AND .anim/limit STZ
	( get maximum frames that can fit in memory )
	#fff0 ;data -- GET-FRAME-SIZE DIV2 #0001 -- NIP
	GTH ,&clamp JCN
	;redraw JSR2
	RTN
	&clamp
		#00 .anim/limit STZ
		;redraw JSR2

RTN

@paint-line ( x1* y1* x2* y2* -- )

	( load ) .line/y1 STZ2 .line/x1 STZ2 .line/y2 STZ2 .line/x2 STZ2

	.line/x1 LDZ2 .line/x2 LDZ2 -- ABS2 .line/dx STZ2
	.line/y1 LDZ2 .line/y2 LDZ2 -- ABS2 #0000 SWP2 -- .line/dy STZ2
	#ffff #00 .line/x2 LDZ2 .line/x1 LDZ2 LTS2 2** ++ .line/sx STZ2
	#ffff #00 .line/y2 LDZ2 .line/y1 LDZ2 LTS2 2** ++ .line/sy STZ2
	.line/dx LDZ2 .line/dy LDZ2 ++ .line/e1 STZ2
	&loop
		( paint ) .line/x2 LDZ2 .line/y2 LDZ2 .brush/oper LDZ2 JSR2
		.line/x2 LDZ2 .line/y2 LDZ2 ;draw-tile JSR2
		.line/x2 LDZ2 .line/x1 LDZ2 == .line/y2 LDZ2 .line/y1 LDZ2 == #0101 == ,&end JCN
		.line/e1 LDZ2 2** .line/e2 STZ2
		.line/e2 LDZ2 .line/dy LDZ2 LTS2 ,&skipy JCN
			.line/e1 LDZ2 .line/dy LDZ2 ++ .line/e1 STZ2
			.line/x2 LDZ2 .line/sx LDZ2 ++ .line/x2 STZ2
			&skipy
		.line/e2 LDZ2 .line/dx LDZ2 GTS2 ,&skipx JCN
			.line/e1 LDZ2 .line/dx LDZ2 ++ .line/e1 STZ2
			.line/y2 LDZ2 .line/sy LDZ2 ++ .line/y2 STZ2
			&skipx
		;&loop JMP2
	&end

RTN

@paint-rect ( -- )

	.cursor/y2 LDZ2 .cursor/y1 LDZ2 ( flip sign ) GTH2k JMP SWP2
	&ver
		STH2k
		.cursor/x2 LDZ2 .cursor/x1 LDZ2 ( flip sign ) GTH2k JMP SWP2
		&hor
			DUP2 STH2kr ;patternize JSR2 #00 = ,&no-pixel JCN
				DUP2 STH2kr .brush/oper LDZ2 JSR2
				&no-pixel
			INC2
			OVR2 OVR2 GTS2 ,&hor JCN
		POP2 POP2 POP2r
		INC2
		OVR2 OVR2 GTS2 ,&ver JCN
	POP2 POP2
	;draw-canvas JSR2

RTN

@paint-brush ( x* y* -- )

	STH2 STH2
	#08 #00
	&ver
		#08 #00
		&hor
			GET-ITER
			DUP2 STH STH ;size-icns #00 .brush/size LDZ 8** ++ #00 STHr ++ LDA #07 STHr SUB SFT #01 AND #00 = ,&skip JCN
			DUP2 [ TOS ROT TOS SWP2 ] STH2kr SWP2r STH2kr SWP2r ADD-POSITIONS OVR2 OVR2 ;patternize JSR2 #00 = ,&no-pixel JCN
					OVR2 OVR2 .brush/oper LDZ2 JSR2
					;draw-tile JSR2
					,&skip JMP
					&no-pixel
					POP2 POP2
				&skip
				POP2
			INC GTHk ,&hor JCN
			POP2
		INC GTHk ,&ver JCN
	POP2
	POP2r POP2r

RTN

@patternize ( x* y* -- )

	STH2 STH2
	.brush/patt LDZ
	DUP ,&noplain JCN
		POP POP2r POP2r
		#01 RTN &noplain
	DUP #01 ! ,&notone1 JCN
		POP OVR2r OVR2r
		STH2r STH2r ++ #0001 AND2 #0000 ==
		STH2r STH2r -- #0001 AND2 #0000 ==
		#0101 == RTN &notone1
	DUP #02 ! ,&notone2 JCN
		POP OVR2r OVR2r
		STH2r STH2r ++ #0003 AND2 #0000 ==
		STH2r STH2r -- #0003 AND2 #0000 ==
		#0101 == RTN &notone2
	DUP #03 ! ,&notone3 JCN
		POP OVR2r OVR2r
		STH2r STH2r ++ #0005 AND2 #0000 ==
		STH2r STH2r -- #0005 AND2 #0000 ==
		#0101 == RTN &notone3
	DUP #04 ! ,&notone4 JCN
		POP STH2r STH2r
		++ #0003 AND2 #0000 == RTN &notone4
	DUP #05 ! ,&notone5 JCN
		POP STH2r STH2r
		-- #0003 AND2 #0000 == RTN &notone5
	DUP #06 ! ,&notone6 JCN
		POP SWP2r POP2r STH2r
		#0001 AND2 NIP RTN &notone6
	DUP #07 ! ,&notone7 JCN
		POP POP2r STH2r
		#0001 AND2 NIP RTN &notone7
	POP POP2r POP2r
	#00

RTN

@paint-magic ( x* y* -- )

	STH2 STH2
	.brush/size LDZ #00
	&ver
		.brush/size LDZ #00
		&hor
			GET-ITER [ TOS ROT TOS ]
			STH2kr SWP2r STH2kr SWP2r ADD-POSITIONS

			( skip blanks )
			OVR2 OVR2 ;get-pixel JSR2 #00 = ,&blank JCN
				OVR2 OVR2 ,clean-pixel JSR
				OVR2 OVR2 ;draw-tile JSR2
				&blank
			POP2 POP2

			INC GTHk ,&hor JCN
			POP2
		INC GTHk ,&ver JCN
	POP2
	POP2r POP2r

RTN

@clean-pixel ( x* y* -- )

	( get neighbors )
	OVR2 OVR2 DEC2 ,get-pixel JSR STH
	OVR2 INC2 OVR2 ,get-pixel JSR #10 SFT STH ADDr
	OVR2 OVR2 INC2 ,get-pixel JSR #20 SFT STH ADDr
	OVR2 DEC2 OVR2 ,get-pixel JSR #30 SFT STH ADDr
	STHr
	DUP #03 = ,&remove JCN
	DUP #06 = ,&remove JCN
	DUP #09 = ,&remove JCN
	DUP #0c = ,&remove JCN
	POP POP2 POP2 RTN
	&remove
		POP ;remove-pixel JSR2 RTN

RTN

@get-pixel ( x* y* -- b )

	,within-canvas JSR ,&continue JCN
		POP2 POP2 #01 RTN
		&continue

	( keep x* ) OVR2 NIP 8MOD STH
	( get byte ) ,get-row JSR LDA
	( flag ) #07 STHr SUB SFT #01 AND

RTN

@add-pixel ( x* y* -- )

	,within-canvas JSR ,&continue JCN
		POP2 POP2 RTN
		&continue

	( keep x* ) OVR2 NIP 8MOD STH
	( get byte ) ,get-row JSR LDAk
	( mask ) #01 #07 STHr SUB SFL ORA
	( save ) ROT ROT STA

RTN

@remove-pixel ( x* y* -- )

	,within-canvas JSR ,&continue JCN
		POP2 POP2 RTN
		&continue

	( keep x* ) OVR2 NIP 8MOD STH
	( get byte ) ,get-row JSR LDAk
	( mask ) #01 #07 STHr SUB SFL #ff EOR AND
	( save ) ROT ROT STA

RTN

@within-canvas ( x* y* -- x* y* flag )

	( clamp ) DUP2 8// .canvas/h LDZ2 << #00 = ,&skip JCN
	( clamp ) OVR2 8// .canvas/w LDZ2 << #00 = ,&skip JCN
	#01 RTN
	&skip
		#00 RTN

RTN

@get-row ( x* y* -- row* )

	STH2k
		8// SWP2
		8// SWP2
		.canvas/w LDZ2 ** ++ 8**
	STH2r
		8MOD2 ++
	.anim/addr LDZ2 ++

RTN

( file )

@new-file ( path* -- )

	#2c ;draw-filepath JSR2
	( rename to untitled.txt )
	;path/name #0020 ;mclr JSR2
	;untitled-txt ;path/name #000c ;mcpy JSR2
	RESET-STATE
	;clear JSR2

RTN

@load-file ( path* -- )

	;path/name .File/name DEO2
	GET-ANIM-SIZE .File/length DEO2
	;data .File/read DEO2

	RESET-STATE

	;draw-canvas JSR2
	;draw-filepane/state JSR2

RTN

@save-file ( path* -- )

	.File/name DEO2
	GET-ANIM-SIZE .File/length DEO2
	;data .File/write DEO2

	RESET-STATE
	;draw-filepane/state JSR2

RTN

( theme )

@theme-txt ".theme $1

@load-theme ( -- )

	;theme-txt .File/name DEO2
	#0006 .File/length DEO2
	#fffa .File/read DEO2

	.File/success DEI2 #0006 !! ,&ignore JCN
		#fffa LDA2 .System/r DEO2
		#fffc LDA2 .System/g DEO2
		#fffe LDA2 .System/b DEO2
		&ignore
	;redraw JSR2

RTN

( Drawing )

@redraw ( -- )

	;draw-background JSR2
	,draw-canvas JSR

RTN

@draw-canvas-outside ( -- )

	( frame )
	.canvas #01 ;line-rect JSR2

	,draw-canvas JSR

RTN

@draw-canvas ( -- )

	( full )
	.zoom/active LDZ ,&zoom JCN
			.anim/onion LDZ ,&full-onion JCN
			#09 ,draw-canvas-full JSR
			RTN
			&full-onion
			( onion )
			;prev-frame JSR2
			#0b ,draw-canvas-full JSR
			;next-frame JSR2
			#05 ,draw-canvas-full JSR
		RTN
		&zoom
	( zoom )
		.anim/onion LDZ ,&zoom-onion JCN
		#09 ,draw-canvas-zoom JSR
		RTN
		&zoom-onion
		( onion )
		;prev-frame JSR2
		#0b ,draw-canvas-zoom JSR
		;next-frame JSR2
		#05 ,draw-canvas-zoom JSR
	RTN

RTN

@draw-canvas-full ( color -- )

	( auto x addr ) #05 .Screen/auto DEO

	STH
	.anim/addr LDZ2 .Screen/addr DEO2

	.canvas/y1 LDZ2 .Screen/y DEO2
	.canvas/h-short LDZ #00
	&ver
		.canvas/x1 LDZ2 .Screen/x DEO2
		.canvas/w-short LDZ #00
		&hor
			STHkr .Screen/sprite DEO
			INC GTHk ,&hor JCN
		POP2
		.Screen/y DEI2 #0008 ++ .Screen/y DEO2
		INC GTHk ,&ver JCN
	POP2
	POPr

	( auto none ) #00 .Screen/auto DEO

RTN

@draw-canvas-zoom ( color -- )

	( auto x ) #01 .Screen/auto DEO

	STH

	.canvas/y1 LDZ2 .Screen/y DEO2
	.canvas/h-short LDZ #00
	&ver
		.canvas/x1 LDZ2 .Screen/x DEO2
		.canvas/w-short LDZ #00
		&hor
			( get pixel )
			GET-ITER
				( y ) TOS .zoom/y LDZ2 ++ STH2
				( x ) TOS .zoom/x LDZ2 ++ STH2r
				;get-pixel JSR2 TOS 8**
				;bigpixel-icns ++ .Screen/addr DEO2
			STHkr .Screen/sprite DEO
			INC GTHk ,&hor JCN
		POP2
		.Screen/y DEI2 #0008 ++ .Screen/y DEO2
		INC GTHk ,&ver JCN
	POP2
	POPr

	( auto none ) #00 .Screen/auto DEO

RTN

@draw-tile ( x* y* -- )

	;within-viewport JSR2 ,&continue JCN
		POP2 POP2 RTN
		&continue
	.zoom/active LDZ #00 = ,&no-zoom JCN
		OVR2 OVR2 ;get-pixel JSR2 #00 SWP 8** ;bigpixel-icns ++ .Screen/addr DEO2
		.zoom/y LDZ2 -- 8** .canvas/y1 LDZ2 ++ #22 SFT2 .Screen/y DEO2
		.zoom/x LDZ2 -- 8** .canvas/x1 LDZ2 ++ #22 SFT2 .Screen/x DEO2
		#09 .Screen/sprite DEO
		RTN
		&no-zoom
	( TODO: Draw onionskin )
	#33 SFT2 SWP2 #33 SFT2 SWP2
	OVR2 OVR2 ;get-row JSR2 .Screen/addr DEO2
	.canvas/y1 LDZ2 ++ .Screen/y DEO2
	.canvas/x1 LDZ2 ++ .Screen/x DEO2
	#09 .Screen/sprite DEO

RTN

@within-viewport ( x* y* -- x* y* flag )

	.zoom/active LDZ #00 = ,&no-zoom JCN
		OVR2 .zoom/x LDZ2 -- .canvas/w LDZ2 << ,&within-y-zoom JCN [ #00 RTN ] &within-y-zoom
		DUP2 .zoom/y LDZ2 -- .canvas/h LDZ2 << ,&within-x-zoom JCN [ #00 RTN ] &within-x-zoom
		#01 RTN
		&no-zoom
	DUP2 .canvas/h LDZ2 8** << ,&within-y JCN [ #00 RTN ] &within-y
	OVR2 .canvas/w LDZ2 8** << ,&within-x JCN [ #00 RTN ] &within-x
		#01 RTN

RTN

@draw-cursor ( -- )

	( clear brush size )
	.cursor/x2 LDZ2
	.cursor/y2 LDZ2
	( clear size cursor )
	OVR2 OVR2
	#0003 -- .Screen/y DEO2
	#0003 -- .Screen/x DEO2
	#40 .Screen/sprite DEO
	( clear last cursor )
	#40 ;draw-pointer JSR2

	( record cursor positions )
	.Mouse/x DEI2 .cursor/x2 STZ2
	.Mouse/y DEI2 .cursor/y2 STZ2

	( draw size cursor )
	.brush/tool LDZ DUP #01 = SWP #03 = #0000 == ;&outside-canvas JCN2
	( outside canvas )
	.Mouse/x DEI2 .Mouse/y DEI2 .canvas ;within-rect JSR2 #00 = ;&outside-canvas JCN2
		.cursor/x2 LDZ2 #0003 -- .Screen/x DEO2
		.cursor/y2 LDZ2 #0003 -- .Screen/y DEO2
		;brush-icns #00 .brush/size LDZ 8** ++ .Screen/addr DEO2
		.Mouse/state DEI #00 ! DUP 2* #41 ADD .Screen/sprite DEO
		#00 = ,&outside-canvas JCN RTN
	&outside-canvas

	( draw new cursor )
	.cursor/x2 LDZ2
	.cursor/y2 LDZ2
	#4143 .Mouse/state DEI SWP? POP
		,draw-pointer JSR

RTN

@draw-pointer ( x* y* color -- )

	STH
	.Screen/y DEO2
	.Screen/x DEO2
	;pointers-icns .Screen/addr DEO2
	STHr .Screen/sprite DEO

RTN

@draw-toolpane ( -- )

	( frame ) .toolpane #01 ;line-rect JSR2

	.toolpane/x1 LDZ2 2++ .Screen/x DEO2
	.toolpane/y1 LDZ2 2++ .Screen/y DEO2
	;tool-icns .Screen/addr DEO2

	( auto x addr ) #05 .Screen/auto DEO
	( stash selection ) .brush/tool LDZ STH
	#06 #00
	&loop
		DUP STHkr = INC .Screen/sprite DEO
		INC GTHk ,&loop JCN
	POP2
	.zoom/active LDZ #01 ! ,&no-zoom JCN
		.Screen/x DEI2 #0008 -- .Screen/x DEO2
		;tool-icns #0030 ++ .Screen/addr DEO2
		#05 STHkr = INC .Screen/sprite DEO
		&no-zoom
	( pop selection ) POPr
	( auto none ) #00 .Screen/auto DEO

RTN

@draw-sizepane ( -- )

	( frame )
	.sizepane #01 ;line-rect JSR2

	.sizepane/x1 LDZ2 2++ .Screen/x DEO2
	.sizepane/y1 LDZ2 2++ .Screen/y DEO2
	;size-icns .brush/mode LDZ 40* TOS ++ .Screen/addr DEO2

	( auto x addr ) #05 .Screen/auto DEO
	( stash selection ) .brush/size LDZ STH
	#08 #00
	&loop
		DUP STHkr = INC .Screen/sprite DEO
		INC GTHk ,&loop JCN
	POP2
	( pop selection ) POPr
	( auto none ) #00 .Screen/auto DEO

RTN

@draw-pattpane ( -- )

	( frame )
	.pattpane #01 ;line-rect JSR2

	.pattpane/x1 LDZ2 2++ .Screen/x DEO2
	.pattpane/y1 LDZ2 2++ .Screen/y DEO2
	;patt-icns .Screen/addr DEO2

	( auto x addr ) #05 .Screen/auto DEO
	( stash selection ) .brush/patt LDZ STH
	#08 #00
	&loop
		DUP STHkr = INC .Screen/sprite DEO
		INC GTHk ,&loop JCN
	POP2
	( pop selection ) POPr
	( auto none ) #00 .Screen/auto DEO

RTN

@draw-animpane ( -- )

	.animpane/y1 LDZ2 .Screen/y DEO2

	.animpane/x1 LDZ2 STH2k .Screen/x DEO2
	;eyes-icns .anim/onion LDZ TOS 8** ++ .Screen/addr DEO2
	#01 .Screen/sprite DEO

	STH2kr #0010 ++ .Screen/x DEO2
	.anim/frame LDZ ;print/parse JSR2 .Screen/addr DEO2
	#02 .Screen/sprite DEO

	STH2r #0018 ++ .Screen/x DEO2
	.anim/limit LDZ ;print/parse JSR2 .Screen/addr DEO2
	#08 .Screen/sprite DEO

RTN

@draw-filepane ( -- )

	( file i/o )
	.filepane/x1 LDZ2 .Screen/x DEO2
	.filepane/y1 LDZ2 .Screen/y DEO2
	;make-icn .Screen/addr DEO2
	#01 .Screen/sprite DEO
	.Screen/x DEI2 #0008 ++ .Screen/x DEO2
	;load-icn .Screen/addr DEO2
	#01 .Screen/sprite DEO
	.Screen/x DEI2 #0010 ++ .Screen/x DEO2

	&state ( -- )
		.filepane/x1 LDZ2 #0018 ++ .Screen/x DEO2
		.filepane/y1 LDZ2 .Screen/y DEO2
		;save-icn .Screen/addr DEO2
		#0a05 .state/changed LDZ SWP? POP .Screen/sprite DEO

RTN

@draw-namepane ( -- )

	( bottom )
	.namepane/x1 LDZ2 .Screen/x DEO2
	.namepane/y1 LDZ2 .Screen/y DEO2
	#01 ;draw-filepath JSR2
	&clear
		#00 .Screen/sprite DEO
		.Screen/x DEI2 #0008 ++ DUP2 .Screen/x DEO2
		.namepane/x2 LDZ2 << ,&clear JCN

RTN

@draw-background ( -- )

	;draw-filepane/state JSR2
	;draw-animpane JSR2
	;draw-filepane JSR2

	.state/presentation LDZ #00 = #01 JCN [ RTN ]

	( draw hor line )
	#0000
	.Screen/width DEI2
	.center/y LDZ2
	.state/presentation LDZ #00 =
		;line-hor JSR2

	( draw ver line )
	.center/x LDZ2
	#0000
	.Screen/height DEI2
	.state/presentation LDZ #00 =
		;line-ver JSR2

	;draw-toolpane JSR2
	;draw-pattpane JSR2
	;draw-sizepane JSR2
	;draw-namepane JSR2

RTN

@draw-filepath ( color -- )

	STH
	#0038 .Screen/x DEO2
	.Screen/height DEI2 #0010 -- .Screen/y DEO2
	;path/name
	&loop
		LDAk DUP #00 ! 20* - TOS 8** ;font ++ .Screen/addr DEO2
		STHkr .Screen/sprite DEO
		.Screen/x DEI2 #0008 ++ .Screen/x DEO2
		INC2 LDAk ,&loop JCN
	POP2
	POPr
	( clear last )
	#00 .Screen/sprite DEO

RTN

( Generics )

@print ( short* color -- )

	&short ( short* color -- )
		ROT OVR ,&byte JSR
		.Screen/x DEI2 #0008 ++ .Screen/x DEO2
		,&byte JSR
	RTN

	&byte ( byte color -- )
		STH DUP
			#04 SFT ,&parse JSR .Screen/addr DEO2
			STHkr .Screen/sprite DEO
		.Screen/x DEI2 #0008 ++ .Screen/x DEO2
			#0f AND ,&parse JSR .Screen/addr DEO2
			STHr .Screen/sprite DEO
	RTN

	&parse ( value -- addr* )
		DUP #09 GTH ,&above JCN
			#30 ADD #20 - TOS 8** ;font ++ RTN
		&above
			#09 SUB #40 ADD #20 - TOS 8** ;font ++ RTN

RTN

@within-rect ( x* y* rect -- flag )

	STH
	( y < rect.y1 ) DUP2 STHkr #02 ADD LDZ2 << ,&skip JCN
	( y > rect.y2 ) DUP2 STHkr #06 ADD LDZ2 >> ,&skip JCN
	SWP2
	( x < rect.x1 ) DUP2 STHkr LDZ2 << ,&skip JCN
	( x > rect.x2 ) DUP2 STHkr #04 ADD LDZ2 >> ,&skip JCN
	POP2 POP2 POPr
	#01
RTN
	&skip
	POP2 POP2 POPr
	#00

RTN

@line-hor ( x0* x1* y* color -- )

	STH .Screen/y DEO2 SWP2
	&loop
		( save ) DUP2 .Screen/x DEO2
		( draw ) STHkr .Screen/pixel DEO
		2++ GTH2k ,&loop JCN
	POP2 POP2 POPr

RTN

@line-ver ( x* y0* y1* color -- )

	STH ROT2 .Screen/x DEO2 SWP2
	&loop
		( save ) DUP2 .Screen/y DEO2
		( draw ) STHkr .Screen/pixel DEO
		2++ GTH2k ,&loop JCN
	POP2 POP2 POPr

RTN

@line-diag ( rect color -- )

	( load ) .line-guide/color STZ
	( x0 ) LDZ2k  #0001 SUB2 .line-guide/x2 STZ2
	( y0 ) INC INC LDZ2k #0001 SUB2  .line-guide/y2 STZ2
	( x1 ) INC INC LDZ2k .line-guide/x1 STZ2
	( y1 ) INC INC LDZ2 .line-guide/y1 STZ2

	.line-guide/x1 LDZ2 .line-guide/x2 LDZ2 -- ABS2 .line-guide/dx STZ2
	.line-guide/y1 LDZ2 .line-guide/y2 LDZ2 -- ABS2 #0000 SWP2 -- .line-guide/dy STZ2
	#ffff #00 .line-guide/x2 LDZ2 .line-guide/x1 LDZ2 LTS2 2** ++ .line-guide/sx STZ2
	#ffff #00 .line-guide/y2 LDZ2 .line-guide/y1 LDZ2 LTS2 2** ++ .line-guide/sy STZ2
	.line-guide/dx LDZ2 .line-guide/dy LDZ2 ++ .line-guide/e1 STZ2
	&loop
		( paint ) .line-guide/x2 LDZ2 .Screen/x DEO2 .line-guide/y2 LDZ2 .Screen/y DEO2 .line-guide/color LDZ .Screen/pixel DEO
		.line-guide/x2 LDZ2 .line-guide/x1 LDZ2 == .line-guide/y2 LDZ2 .line-guide/y1 LDZ2 == #0101 == ,&end JCN
		.line-guide/e1 LDZ2 2** .line-guide/e2 STZ2
		.line-guide/e2 LDZ2 .line-guide/dy LDZ2 LTS2 ,&skipy JCN
			.line-guide/e1 LDZ2 .line-guide/dy LDZ2 ++ .line-guide/e1 STZ2
			.line-guide/x2 LDZ2 .line-guide/sx LDZ2 ++ .line-guide/x2 STZ2
			&skipy
		.line-guide/e2 LDZ2 .line-guide/dx LDZ2 GTS2 ,&skipx JCN
			.line-guide/e1 LDZ2 .line-guide/dx LDZ2 ++ .line-guide/e1 STZ2
			.line-guide/y2 LDZ2 .line-guide/sy LDZ2 ++ .line-guide/y2 STZ2
			&skipx
		;&loop JMP2
	&end

RTN

@line-rect ( rect color -- )

	STH STH

	( not empty )
	STHkr LDZ2 STHkr #04 + LDZ2 !! ,&has-width JCN
		POP2r RTN
		&has-width
	STHkr #02 + LDZ2 STHkr #06 + LDZ2 !! ,&has-height JCN
		POP2r RTN
		&has-height

	( y2 ) STHkr #06 + LDZ2
	( y1 ) STHkr #02 + LDZ2 #0001 -- ( flip sign ) GTH2k JMP SWP2
	&ver
		( save ) DUP2 .Screen/y DEO2
		( x1 ) STHkr LDZ2 #0001 -- .Screen/x DEO2
		OVRr STHr .Screen/pixel DEO
		( x2 ) STHkr #04 + LDZ2 .Screen/x DEO2
		OVRr STHr .Screen/pixel DEO
		( incr )
		INC2 GTH2k ,&ver JCN
	POP2
	( x2 ) STHkr #04 + LDZ2
	( x1 ) STHkr LDZ2 #0001 -- ( flip sign ) GTH2k JMP SWP2
	&hor
		( save ) DUP2 .Screen/x DEO2
		( y1 ) STHkr #02 + LDZ2 #0001 -- .Screen/y DEO2
		OVRr STHr .Screen/pixel DEO
		( y2 ) STHkr #06 + LDZ2 .Screen/y DEO2
		OVRr STHr .Screen/pixel DEO
		( incr )
		INC2 GTH2k ,&hor JCN
	POP2
	POPr
	.Screen/x DEO2
	.Screen/y DEO2
	STHr .Screen/pixel DEO

RTN

@fill-rect ( rect color -- )

	STH STH
	( y2 ) STHkr #06 + LDZ2 2++
	( y1 ) STHkr #02 + LDZ2 #0001 --
	&ver
		( save ) DUP2 .Screen/y DEO2
		( x2 ) STHkr #04 + LDZ2 2++
		( x1 ) STHkr LDZ2 #0001 --
		&hor
			( save ) DUP2 .Screen/x DEO2
			( draw ) OVRr STHr .Screen/pixel DEO
			INC2 GTH2k ,&hor JCN
		POP2 POP2
		INC2 GTH2k ,&ver JCN
	POP2 POP2
	POPr POPr

RTN

@glyph-snarf ( -- )

	( a col is 4 bytes )
	;snarf-txt .File/name DEO2
	#0001 .File/length DEO2

	.canvas/h LDZ2 8** #0000
	&ver
		.canvas/w LDZ2 8** #0000
		&hor
			( get x,y ) STH2 OVR2 STH2r SWP2 OVR2 SWP2 
			( write ) ,write-col JSR
			INC2 GTH2k ,&hor JCN
		POP2 POP2
		( linebreak ) ;&lb .File/write DEO2
		#0010 ++ GTH2k ,&ver JCN
	POP2 POP2

RTN
	&lb 0a

@write-col ( x* y* -- )

	;&y STA2 ;&x STA2

	( create bytes )
	#0400
	&loop
		( y ) DUP #40 SFT #80 + STH
		#0400
		&loop-pixels
			( pixel ) ;&x LDA2 ;&y LDA2 ;get-pixel JSR2
			( merge ) OVR #40 SFT SFT STH ADDr
			( incr ) ;&y LDA2k INC2 SWP2 STA2
			INC GTHk ,&loop-pixels JCN
		POP2
		( create ) STHr 
		( write ) OVR #00 SWP ;&bytes ++ STA 
		INC GTHk ,&loop JCN
	POP2

	( set advance bit to last byte with pixels )
	#0004
	&loop-count
		DUP #00 SWP ;&bytes ++ LDA #0f AND ,&found JCN
		#01 - LTHk ,&loop-count JCN
	&found

	( store advance bit ) NIP #00 SWP ;&bytes ++ LDAk #40 + ROT ROT STA

	( write )
	#0400
	&loop-write
		DUP #00 SWP ;&bytes ++
		LDAk #80 = ,&skip JCN
		LDAk #90 = ,&skip JCN
		LDAk #a0 = ,&skip JCN
		LDAk #b0 = ,&skip JCN
		DUP2 .File/write DEO2
		&skip
		POP2
		INC GTHk ,&loop-write JCN
	POP2

RTN
	&x $2 &y $2 &bytes $4 $1

@snarf-txt ".snarf $1

( Tools )

@tools
	:pencil/hover  :pencil/up  :pencil/down  :pencil/drag
	:marker/hover  :marker/up  :marker/down  :marker/drag
	:stroke/hover  :stroke/up  :stroke/down  :stroke/drag
	:magic/hover   :magic/up   :magic/down   :magic/drag
	:box/hover     :box/up     :box/down     :box/drag
	:magnify/hover :magnify/up :magnify/down :magnify/drag

@pencil ( x* y* -> )

	&down
		OVR2 OVR2 .cursor/y1 STZ2 .cursor/x1 STZ2
		OVR2 OVR2 .brush/oper LDZ2 JSR2
	&up
		;draw-tile JSR2
		SET-STATE
		BRK
	&drag
		OVR2 OVR2 STH2 STH2
		.cursor/x1 LDZ2 .cursor/y1 LDZ2 STH2r STH2r
		;paint-line JSR2
		.cursor/y1 STZ2 .cursor/x1 STZ2
		BRK
	&hover

BRK

@marker ( x* y* -> )

	&up
		POP2 POP2
		SET-STATE BRK
	&down
	&drag
		#0003 -- SWP2 #0003 -- SWP2
		;paint-brush JSR2
		BRK
	&hover

BRK

@stroke ( x* y* -> )

	&down
		( clear cursor ) .cursor/x2 LDZ2 .cursor/y2 LDZ2 #40 ;draw-pointer JSR2
		( erase guide ) .guide #40 ;line-diag JSR2
		.cursor/y1 STZ2 .cursor/x1 STZ2
		BRK
	&up
		( clear cursor ) .cursor/x2 LDZ2 .cursor/y2 LDZ2 #40 ;draw-pointer JSR2
		( erase guide ) .guide #40 ;line-diag JSR2
		.cursor/y2 STZ2 .cursor/x2 STZ2
		.cursor/x1 LDZ2 .cursor/y1 LDZ2 .cursor/x2 LDZ2 .cursor/y2 LDZ2 ;paint-line JSR2
		SET-STATE
		BRK
	&drag
		( clear cursor ) .cursor/x2 LDZ2 .cursor/y2 LDZ2 #40 ;draw-pointer JSR2
		( erase guide ) .guide #40 ;line-diag JSR2
		.cursor/y2 STZ2 .cursor/x2 STZ2
		( update guide )
		.cursor/x1 LDZ2 .cursor/y1 LDZ2 ;to-canvas JSR2 INC2 .guide/y1 STZ2 INC2 .guide/x1 STZ2
		.cursor/x2 LDZ2 .cursor/y2 LDZ2 ;to-canvas JSR2 .guide/y2 STZ2 .guide/x2 STZ2
		( erase guide ) .guide #43 ;line-diag JSR2
		BRK
	&hover

BRK

@magic ( x* y* -> )

	&up
		SET-STATE
	&down
	&drag
		#0003 -- SWP2 #0003 -- SWP2
		;paint-magic JSR2
		BRK
	&hover

BRK

@box ( x* y* -> )

	&down
		( clear cursor ) .cursor/x2 LDZ2 .cursor/y2 LDZ2 #40 ;draw-pointer JSR2
		( erase guide ) .guide #40 ;line-rect JSR2
		.cursor/y1 STZ2 .cursor/x1 STZ2
		BRK
	&up
		( clear cursor ) .cursor/x2 LDZ2 .cursor/y2 LDZ2 #40 ;draw-pointer JSR2
		( erase guide ) .guide #40 ;line-rect JSR2
		.cursor/y2 STZ2 .cursor/x2 STZ2
		;paint-rect JSR2
		SET-STATE
		BRK
	&drag
		( clear cursor ) .cursor/x2 LDZ2 .cursor/y2 LDZ2 #40 ;draw-pointer JSR2
		( erase guide ) .guide #40 ;line-rect JSR2
		.cursor/y2 STZ2 .cursor/x2 STZ2
		( update guide )
		.cursor/x1 LDZ2 .cursor/y1 LDZ2 ,to-canvas JSR INC2 .guide/y1 STZ2 INC2 .guide/x1 STZ2
		.cursor/x2 LDZ2 .cursor/y2 LDZ2 ,to-canvas JSR .guide/y2 STZ2 .guide/x2 STZ2
		( draw guide ) .guide #43 ;line-rect JSR2
		BRK
	&hover

BRK

@to-canvas ( x* y* -- x* y* )

	.zoom/active LDZ ,&on-zoom JCN
		.canvas/y1 LDZ2 ++ SWP2
		.canvas/x1 LDZ2 ++ SWP2
	RTN

	&on-zoom
		.zoom/y LDZ2 -- 8** .canvas/y1 LDZ2 ++ SWP2
		.zoom/x LDZ2 -- 8** .canvas/x1 LDZ2 ++ SWP2

RTN

@magnify ( x* y* -> )

	&down
		.zoom/active TOGGLE
		.canvas/h LDZ2 2// -- .zoom/y STZ2
		.canvas/w LDZ2 2// -- .zoom/x STZ2
		;redraw JSR2
		BRK
	&up
	&drag
		POP2 POP2
		BRK
	&hover

BRK

( memory generics )

@mclr ( addr* len* -- )

	OVR2 ++ SWP2
	&loop
		STH2k #00 STH2r STA
		INC2 GTH2k ,&loop JCN
	POP2 POP2

RTN

@mcpy ( src* dst* len* -- )

	SWP2 STH2
	OVR2 ++ SWP2
	&loop
		LDAk STH2kr STA INC2r
		INC2 GTH2k ,&loop JCN
	POP2 POP2
	POP2r

RTN

( string generics )

@slen ( str* -- len* )

	DUP2 ,scap JSR SWP2 --

RTN

@scap ( str* -- str-end* )

	( clamp ) LDAk #00 ! JMP RTN
	&while INC2 LDAk ,&while JCN

RTN

@sput ( str* char -- )

	ROT ROT ,scap JSR STA

RTN

@spop ( str* -- )

	( clamp ) LDAk #00 ! JMP RTN
	#00 ROT ROT ,scap JSR #0001 -- STA

RTN

@print-hex ( value* -- )
	
	SWP ,&byte JSR 
	&byte ( byte -- )
		STHk #04 SFT ,&parse JSR #18 DEO
		STHr #0f AND ,&parse JSR #18 DEO
	RTN
	&parse ( byte -- char ) DUP #09 GTH ,&above JCN #30 + RTN 
	&above #57 + RTN

RTN

@untitled-txt  "untitled.icn $1

~src/assets.tal

@data
