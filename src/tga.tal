( tga )

%FLIP { STH2k LDA2 SWP STH2r STA2 }

@load-file-tga ( -- )

	( header )
	;filepath .File/name DEO2
	#0012 .File/length DEO2
	;image .File/read DEO2
	( flip endianness )
	;image/w FLIP ;image/h FLIP
	( resize canvas )
	;image/w LDA2 8// NIP ;image/h LDA2 8// NIP ;set-surface-size JSR2
	( get parser )
	;image/image-type LDA
	#02 !~ ,&no-raw-true JCN
		#0004 ;parse-tga/length STA2
		;pixel-raw-true ;parse-tga/filter STA2
		,parse-tga JSR
		POP RTN
		&no-raw-true
	#03 !~ ,&no-raw-bw JCN
		#0001 ;parse-tga/length STA2
		;pixel-raw-bw ;parse-tga/filter STA2
		,parse-tga JSR
		POP RTN
		&no-raw-bw
	( error )
	;&error-unknown-txt ;print-str JSR2
	;image/image-type LDA 2* TOS ;image-types ++ LDA2 PRINT

RTN
	&error-unknown-txt "Unsupported 20 "tga-type: 20 $1

@parse-tga ( -- )

	AUTO-X
	( stream )
	[ LIT2 &length $2 ] .File/length DEO2
	#0000 ,&x STR2
	#0000 ,&y STR2
	&stream
		;&pixel STH2k .File/read DEO2
		[ LIT2 &x $2 ] [ LIT2 &y $2 ] STH2r [ LIT2 &filter $2 ] JSR2 80/ ;set-pixel JSR2
		( inc x )
		,&x LDR2 INC2 ,&x STR2
		( inc y )
		,&x LDR2 ;image/w LDA2 !! ,&continue JCN
			#0000 ,&x STR2
			,&y LDR2 INC2 ,&y STR2
			&continue
		.File/success DEI2 #0000 !! ,&stream JCN
	AUTO-NONE
	( rename )
	;&icn-ext ;filepath ;scap JSR2 #0003 -- ;scpy JSR2
	;draw-surface JSR2

RTN
	&pixel $4
	&icn-ext "icn $1

( 1bpp pixel filters )

@pixel-raw-true ( pixel* -- color )

	STH2
	( b ) #00 STH2kr LDA INC2r
	( g ) #00 STH2kr LDA INC2r
	( r ) #00 STH2r LDA
	( res ) ++ ++ #0003 // NIP

RTN

@pixel-raw-bw ( pixel* -- color )

	( res ) LDA

RTN

@image-types-txts
    &no-image "no-image-data-is-present $1
    &raw-color "RAW-color $1
    &raw-true "RAW-true $1
    &raw-bw "RAW-bw $1
    &rle-color "RLE-color $1
    &rle-true "RLE-true $1
    &rle-bw "RLE-bw $1
    &unknown "unknown $1

@image-types
	:image-types-txts/no-image
	:image-types-txts/raw-color
	:image-types-txts/raw-true
	:image-types-txts/raw-bw
	:image-types-txts/unknown
	:image-types-txts/unknown
	:image-types-txts/unknown
	:image-types-txts/unknown
	:image-types-txts/unknown
	:image-types-txts/rle-color
	:image-types-txts/rle-true
	:image-types-txts/rle-bw

@image
	&id-length $1
	&color-map $1
	&image-type $1
	&map $5
	&x $2 &y $2
	&w $2 &h $2
	&depth $1
	&descriptor $1
